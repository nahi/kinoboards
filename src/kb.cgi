#!/usr/local/bin/perl
#
# $Id: kb.cgi,v 1.10 1995-12-15 14:21:37 nakahiro Exp $
#
# $Log: kb.cgi,v $
# Revision 1.10  1995-12-15 14:21:37  nakahiro
# keyword search routine.
#
# Revision 1.9  1995/12/13 17:08:19  nakahiro
# '(single-quote)-char escape routine.
#
# Revision 1.8  1995/12/04 11:44:31  nakahiro
# articles can include ' char.
#
# Revision 1.7  1995/11/24 17:29:00  nakahiro
# title list of picked articles.
#
# Revision 1.6  1995/11/22 13:01:39  nakahiro
# partial sort by date.
#
# Revision 1.5  1995/11/15 11:39:16  nakahiro
# show user-alias information when posting.
#
# Revision 1.4  1995/11/08 09:18:22  nakahiro
# Add reference to Original File when Quoting local file.
#
# Revision 1.3  1995/11/02 05:50:48  nakahiro
# New Feature: quote a local file.
#
# Revision 1.2  1995/11/02 04:59:44  nakahiro
# A bug in SortArticle was fixed.
#
# Revision 1.1  1995/11/01 06:27:56  nakahiro
# many many changes and fixed bugs from ver 1.0.
#
# Revision 1.0  1995/10/22  08:59:03  nakahiro
# beta version.
# released on MAGARI page in kinotrope.
#


# kinoBoards: Kinoboards Is Network Opened BOARD System


#/////////////////////////////////////////////////////////////////////


# ToDo:
#	×	SubjectをRe:にする
#	×	In-Reply-To:をつける
#	×	「〜にフォローされています」をつける。
#	×	user alias
#	×	board-name alias
#	×	indent
#	×	indentの並べ替え
#	×	Error関数
#	×	タイトルリストのフォーマット
#	×	「上の記事に反応する」なんか変。
#	×	HTML or Plain Text
#	×	^Mを取り除く
#	×	最新の記事n個!
#	×	まとめ読み(thread)
#	×	指定した日記を引用する。
#	×	指定した日記へのReferenceをつける
#	×	部分日付ソート
#	×	記事中に'を入れられるようにする
#	×	記事検索機能
#	×	まとめ読みの時、threadをわかりやすくする工夫を
#		「上へ」「下へ」のリンク機能の追加(次/前は廃止?)
#		中身はEUC、ファイルはJISで
#		aliasの登録機能
#		Subjectの先頭にIconをつけたい
#		Boardを自由に選択する
#		新しい記事n個にマークをつける(aging機能との兼ね合いでつらい)
#		記事のキャンセル機能(aging機能との兼ね合いでつらい)


#/////////////////////////////////////////////////////////////////////


###
## ヘッダファイルの読み込み
#
require('kb.ph');


###
## メイン
#

MAIN: {

	local($Command, $Id, $File, $Num, $Type, $Key);

	# 標準入力(POST)または環境変数(GET)のデコード。
	&cgi'decode;
	$Command = $cgi'tags{'c'};
	$Id = $cgi'tags{'id'};
	$File = $cgi'tags{'file'};
	$Num = $cgi'tags{'num'};
	$Type = $cgi'tags{'type'};
	$Key = $cgi'tags{'key'};

	#	新規:			c=n
	#	引用つきフォロー:	c=q&id={[1-9][0-9]*}
	#	引用なしフォロー:	c=f&id={[1-9][0-9]*}
	#	ファイル引用フォロー:	c=q/f&file={filename}
	#	記事のプレビュー:	c=p&(空)....
	#	確認済み画面:		c=x&id={[1-9][0-9]*(引用でない時id=0)}
	#	日付順ソート:		c=r&type=all|new
	#	最新の記事n個:		c=l&num={[1-9][0-9]*}
	#	threadまとめ読み:	c=t&id={[1-9][0-9]*}
	#	検索:			c=s&type=all|new&key={keyword}

	&Entry($NO_QUOTE, 0),			last MAIN if ($Command eq "n");
	$Id ? &Entry($QUOTE_ON, $Id) : &FileEntry($QUOTE_ON, $File),
						last MAIN if ($Command eq "q");
	$Id ? &Entry($NO_QUOTE, $Id) : &FileEntry($NO_QUOTE, $File),
						last MAIN if ($Command eq "f");
	&Thanks($File, $Id),			last MAIN if ($Command eq "x");
	&Preview,				last MAIN if ($Command eq "p");
	&SortArticle($Type),			last MAIN if ($Command eq "r");
	&NewArticle($Num),			last MAIN if ($Command eq "l");
	&ThreadArticle($Id),			last MAIN if ($Command eq "t");
	&SearchArticle($Type, $Key),		last MAIN if ($Command eq "s");

	print("something illegal\n");
}


###
## おしまい
#
exit 0;


#/////////////////////////////////////////////////////////////////////


#
# サブルーチン
#


###
## 書き込み画面
#
sub Entry {

	# 引用あり/なしと、引用する場合はそのId(引用しない時は0)
	local($QuoteFlag, $Id) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);
	# Board名称の取得
	local($BoardName) = &GetBoardInfo($Board);

	# 表示画面の作成
	&MsgHeader($ENTRY_MSG);

	# フォローの場合
	if ($Id != 0) {
		&ViewOriginalArticle($Id, $Board);
		print("<hr>\n");
		print("<h2>上の記事に反応する</h2>");
	}

	# お約束
	print("<form action=\"$PROGRAM\" method =\"POST\">\n");
	print("<input name=\"c\" type=\"hidden\" value=\"p\">\n");
	print("<input name=\"board\" type=\"hidden\" value=\"$Board\">\n");

	# 引用Id; 引用でないなら0。
	print("<input name=\"id\" type=\"hidden\" value=\"$Id\">\n");

	# あおり文
	print("<p>$H_AORI</p>\n");

	# TextType
	print("$H_TEXTTYPE\n");
	print("<SELECT NAME=\"texttype\">\n");
	print("<OPTION SELECTED>$H_PRE\n");
	print("<OPTION>$H_HTML\n");
	print("</SELECT><BR>\n");

	# Board名; 本当は自由に選択できるようにしたい。
	print("$H_BOARD $BoardName<br>\n");

	# Subject(フォローなら自動的に文字列を入れる)
	if ($Id != 0) {
		printf("$H_SUBJECT <input name=\"subject\" value=\"%s\" size=\"$SUBJECT_LENGTH\"><br>\n", &GetReplySubject($Id, $Board));
	} else {
		print("$H_SUBJECT <input name=\"subject\" value=\"\" size=\"$SUBJECT_LENGTH\"><br>\n");
	}

	# 本文(引用ありなら元記事を挿入)
	print("<textarea name=\"article\" rows=\"$TEXT_ROWS\" cols=\"$TEXT_COLS\">\n");
	&QuoteOriginalArticle($Id, $Board)
		if ($Id != 0 && $QuoteFlag == $QUOTE_ON);
	print("</textarea><br>\n");

	# 名前とメールアドレス、URL。
	print("$H_FROM <input name=\"name\" size=\"$NAME_LENGTH\"><br>\n");
	print("$H_MAIL <input name=\"mail\" size=\"$MAIL_LENGTH\"><br>\n");
	print("URL(空でもOK):<input name=\"url\" value=\"http://\" size=\"$URL_LENGTH\"><br>\n");

	print("<p><a href=\"$USER_ALIAS_FILE_URL\">ここ</a>に登録されている方は、「$H_FROM」に「#...」と書くと、自動的に補完されます。登録は<a href=\"mailto:$Maint\">$Maint</a>まで、メールにて御連絡下さい。</p>\n");
	print("<p>入力できましたら、\n");
	print("<input type=\"submit\" value=\"ここ\">\n");
	print("を押して記事を確認しましょう(まだ投稿しません)。</p>\n");

	# お約束
	print("</form>\n");

	&MsgFooter;
}


###
## 書き込み画面(option: ファイルから引用)
#
sub FileEntry {

	# 引用あり/なしと、引用するファイル(kb.cgiから相対)
	local($QuoteFlag, $File) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);
	# Board名称の取得
	local($BoardName) = &GetBoardInfo($Board);

	# 表示画面の作成
	&MsgHeader($ENTRY_MSG);

	# 引用ファイルの表示
	&ViewOriginalFile($File);
	print("<hr>\n");
	print("<h2>上の記事に反応する</h2>");

	# お約束
	print("<form action=\"$PROGRAM\" method =\"POST\">\n");
	print("<input name=\"c\" type=\"hidden\" value=\"p\">\n");
	print("<input name=\"board\" type=\"hidden\" value=\"$Board\">\n");

	# 引用Id; 正規の引用でないので0。
	print("<input name=\"id\" type=\"hidden\" value=\"0\">\n");

	# 引用ファイル
	print("<input name=\"file\" type=\"hidden\" value=\"$File\">\n");

	# あおり文
	print("<p>$H_AORI</p>\n");

	# TextType
	print("$H_TEXTTYPE\n");
	print("<SELECT NAME=\"texttype\">\n");
	print("<OPTION SELECTED>$H_PRE\n");
	print("<OPTION>$H_HTML\n");
	print("</SELECT><BR>\n");

	# Board名; 本当は自由に選択できるようにしたい。
	print("$H_BOARD $BoardName<br>\n");

	# Subject
	printf("$H_SUBJECT <input name=\"subject\" value=\"%s\" size=\"$SUBJECT_LENGTH\"><br>\n", &GetReplySubjectFromFile($File));

	# 本文(引用ありなら元記事を挿入)
	print("<textarea name=\"article\" rows=\"$TEXT_ROWS\" cols=\"$TEXT_COLS\">\n");
	&QuoteOriginalFile($File) if ($QuoteFlag == $QUOTE_ON);
	print("</textarea><br>\n");

	# 名前とメールアドレス、URL。
	print("$H_FROM <input name=\"name\" size=\"$NAME_LENGTH\"><br>\n");
	print("$H_MAIL <input name=\"mail\" size=\"$MAIL_LENGTH\"><br>\n");
	print("URL(空でもOK):<input name=\"url\" value=\"http://\" size=\"$URL_LENGTH\"><br>\n");

	print("<p><a href=\"$USER_ALIAS_FILE_URL\">ここ</a>に登録されている方は、「$H_FROM」に「#...」と書くと、自動的に補完されます。登録は<a href=\"mailto:$Maint\">$Maint</a>まで、メールにて御連絡下さい。</p>\n");
	print("<p>入力できましたら、\n");
	print("<input type=\"submit\" value=\"ここ\">\n");
	print("を押して記事を確認しましょう(まだ投稿しません)。</p>\n");

	# お約束
	print("</form>\n");

	&MsgFooter;
}


###
## プレビュー画面
#
sub Preview {

	# Boardの取得
	local($Board) = $cgi'tags{'board'};

	# TextTypeの取得
	local($TextType) = $cgi'tags{'texttype'};

	# テンポラリファイルの作成
	local($TmpFile) = &MakeTemporaryFile($Board, $TextType);

	# 表示画面の作成
	&MsgHeader($PREVIEW_MSG);

	# お約束
	print("<form action=\"$PROGRAM/$Board\" method =\"GET\">\n");
	print("<input name=\"file\" type=\"hidden\" value=\"$TmpFile\">\n");
	print("<input name=\"c\" type=\"hidden\" value=\"x\">\n");
	printf("<input name=\"id\" type=\"hidden\" value=\"%d\">\n",
		$cgi'tags{'id'});

	# あおり文
	print("<p>以下の記事を確認したら、");
	print("<input type=\"submit\" value=\"ここ\">");
	print("を押して書き込んで下さい。</p>\n");

	# 確認する記事の表示
	open(TMP, "<$TmpFile");
	while(<TMP>) {
		print($_);
	}

	# お約束
	print("</form>\n");

	&MsgFooter;

}


###
## 登録後画面
#
sub Thanks {

	# テンポラリファイル名と引用した記事のId
	local($TmpFile, $Id) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);

	# 登録ファイルのURL
	local($TitleFileURL)
		= $PROGRAM_DIR_URL . "/" . $Board . "/" . $TITLE_FILE_NAME;

	# 新たに記事を生成し、フォローされた記事にその旨書き込む。
	&MakeNewArticle($TmpFile, $Board, $Id);

	# 表示画面の作成
	&MsgHeader($THANKS_MSG);

	print("<p>書き込みの訂正、取消などはメールでお願いいたします。</p>");
	print("<form action=\"$TitleFileURL\">\n");
	print("<input type=\"submit\" value=\"リストを見る\">\n");
	print("</form>\n");

	&MsgFooter;
}


###
## 日付順にソート。新しいものが上。
#
sub SortArticle {

	# タイプ(all / new)
	local($Type) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);
	# file
	local($File) = ($Type eq 'new')
			? "$Board/$TITLE_FILE_NAME"
			: "$Board/$ALL_FILE_NAME";

	local(@lines);

	open(ALL, "$File") || &MyFatal(1, $File);
	while(<ALL>) {
		(/^<li>/) && (s/href=\"/href=\"$PROGRAM_DIR_URL\/$Board\//)
			&& push(@lines, $_);
	}
	close ALL;

	# 表示画面の作成
	&MsgHeader($SORT_MSG);
	print("<ol>\n");
	foreach (reverse sort MyArticleSort @lines) {
		print $_;
	}
	print("</ol>\n");
	&MsgFooter;
}


###
## 新しい記事からn個を表示。
#
sub MyArticleSort {
	local($MyA, $MyB) = ($a, $b);
	$MyA =~ s/<li><strong>([0-9]*) .*$/$1/;
	$MyB =~ s/<li><strong>([0-9]*) .*$/$1/;
	return($MyA <=> $MyB);
}

###
## 新しい記事からn個を表示。
#
sub NewArticle {

	# 表示する個数を取得
	local($Num) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);

	# 記事番号を収めるファイル
	local($ArticleNumFile) = $Board . "/" . $ARTICLE_NUM_FILE_NAME;

	# 最新記事番号を取得
	$ArticleToId = &GetArticleId($ArticleNumFile);

	# 取ってくる最初の記事番号を取得
	$ArticleFromId = $ArticleToId - $Num + 1;

	local($i, $File);

	# 表示画面の作成
	&MsgHeader($NEWARTICLE_MSG);

	print("<p>記事数: $Num ($ArticleToId 〜 $ArticleFromId)</p>");

	# nameへのリンクを表示
	print("<p> //\n");
	for ($i = $ArticleToId; ($i >= $ArticleFromId); $i--) {
		print("<a href=\"\#$i\">$i</a> //\n");
	}
	print("</p>\n");

	for ($i = $ArticleToId; ($i >= $ArticleFromId); $i--) {
		print("<br><hr><br>\n");
		print("<strong><a name=\"$i\">ID = $i</a></strong><br>\n");
		&ViewOriginalArticle($i, $Board);
	}

	&MsgFooter;
}


###
## フォロー記事を全て表示。
#
sub ThreadArticle {

	# 元記事のIdを取得
	local($Id) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);

	# 表示画面の作成
	&MsgHeader($THREADARTICLE_MSG);

	# メイン関数の呼び出し(記事概要)
	print("<ul>\n");
	&ThreadArticleMain('subject only', $Id, $Board);
	print("</ul>\n");

	# メイン関数の呼び出し(記事)
	&ThreadArticleMain('', $Id, $Board);

	&MsgFooter;
}


###
## 再帰的にその記事のフォローを表示する。
#
sub ThreadArticleMain {

	# IdとBoardの取得
	local($SubjectOnly, $Id, $Board) = @_;

	# フォロー記事のIdの取得
	local(@FollowIdList) = &GetFollowIdList($Id, $Board);

	# 記事概要か、記事そのものか。
	if ($SubjectOnly) {

		# 記事概要の表示
		&PrintAbstract($Id, $Board);

	} else {

		# 区切り
		print("<hr>\n");

		# 元記事の表示
		print("<strong><a name=\"$Id\">ID = $Id</a></strong><br>\n");
		&ViewOriginalArticle($Id, $Board);

	}

	# フォロー記事の表示
	foreach (@FollowIdList) {

		# 記事概要なら箇条書
		print("<ul>\n") if ($SubjectOnly);

		# 再帰
		&ThreadArticleMain($SubjectOnly, $_, $Board);

		# 記事概要なら箇条書閉じ
		print("</ul>\n") if ($SubjectOnly);

	}
}


###
## 記事の概要の表示
#
sub PrintAbstract {

	# IdとBoard
	local($Id, $Board) = @_;

	# 引用するファイル
	local($File) = &GetArticleFileName($Id, $Board);

	# 題、日付、名前
	local($Subject, $InputDate, $Name);

	# 記事ファイルからSubject等を取り出す
	open(TMP, "$File") || &MyFatal(1, $File);
	while(<TMP>) {

		# subjectを取り出す。
		if (/^<strong>$H_SUBJECT<\/strong> (.*)<br>$/) {$Subject = $1; }
		# 日付を取り出す。
		if (/^<strong>$H_DATE<\/strong> (.*)<br>$/) {$InputDate = $1; }
		# 名前を取り出す。
		if (/^<strong>$H_FROM<\/strong> <a[^>]*>(.*)<\/a><br>$/) {
			$Name = $1;
		} elsif (/^<strong>$H_FROM<\/strong> (.*)<br>$/) {
			$Name = $1;
		}

	}
	close TMP;

	print("<li><strong>$Id .</strong> <a href=\"\#$Id\">$Subject</a> [$Name] $InputDate\n");

}


###
## 記事の検索(表示画面作成)
#
sub SearchArticle {

	# all/new、キーワード
	local($Type, $Key) = @_;

	# 選択されたBoardの取得
	local($Board) = substr($ENV{'PATH_INFO'}, $[ + 1);

	# 表示画面の作成
	&MsgHeader($SEARCHARTICLE_MSG);

	# お約束
	print("<form action=\"$PROGRAM/$Board\" method =\"GET\">\n");
	print("<input name=\"c\" type=\"hidden\" value=\"s\">\n");
	print("<input name=\"type\" type=\"hidden\" value=\"$Type\">\n");

	# キーワード入力部
	print("<p>キーワードを入力したら、");
	print("<input type=\"submit\" value=\"ここ\">");
	print("を押して下さい。</p>\n");
	print("<p>検索するキーワード:\n");
	print("<input name=\"key\" size=\"$KEYWORD_LENGTH\"></p>\n");
	print("<hr>\n");

	# キーワードが空でなければ、そのキーワードを含む記事のリストを表示
	&SearchArticleList($Board, $Type, $Key) unless ($Key eq "");

	&MsgFooter;
}


###
## 記事の検索(検索結果の表示)
#
sub SearchArticleList {

	# ボード名、all/new、キーワード
	local($Board, $Type, $Key) = @_;

	# 検索対象ファイル
	local($File) = ($Type eq 'new')
			? "$Board/$TITLE_FILE_NAME"
			: "$Board/$ALL_FILE_NAME";
	local($Title, $ArticleFile, $HitFlag, $Line);
	$HitFlag = 0;

	# リスト開く
	print("<dl>\n");

	# ファイルを開く
	open(TITLE, "$File") || &MyFatal(1, $File);
	while(<TITLE>) {
		$Title = $_;
		next unless (/^<li>.*href=\"([^\"]*)\"/);
		$ArticleFile = "$Board/$1";
		$Line = &SearchArticleKeyword($ArticleFile, $Key);
		if ($Line ne "") {
			$Title =~ s/^<li>//go;
			$Title =~ s/href=\"/href=\"$PROGRAM_DIR_URL\/$Board\//;
			$Line =~ s/<[^>]*>//go;
			$Line =~ s/&/&amp;/go;
			$Line =~ s/\"/&quot;/go;
			print("<dt>$Title\n");
			print("<dd>$Line\n");
			$HitFlag = 1;
		}
	}
	close(TITLE);

	# ヒットしなかったら
	print("<dt>該当する記事は見つかりませんでした。\n")
		unless ($HitFlag = 1);

	# リスト閉じる
	print("</dl>\n");
}


###
## 記事の検索(検索結果メイン)
#
sub SearchArticleKeyword {

	# ファイル名とキーワード
	local($File, $Key) = @_;

	# 検索する
	open(ARTICLE, "$File") || &MyFatal(1, $File);
	while(<ARTICLE>) {
		# ヒット?
		(/$Key/) && return($_);
	}

	# ヒットせず
	return("");
}


###
## 確認用テンポラリファイルを作成してファイル名を返す。
#
sub MakeTemporaryFile {

	# BoardとTextTypeの取得
	local($Board, $TextType) = @_;

	# Board名称の取得
	local($BoardName) = &GetBoardInfo($Board);

	# テンポラリファイル名の取得
	local($TmpFile) = "$Board/.$ARTICLE_PREFIX.$$";

	# 日付を取り出す。
	local($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst)
		= localtime(time);
	local($InputDate)
		= sprintf("%d月%d日%02d時%02d分", $mon + 1, $mday, $hour, $min);

	# ホスト名を取り出す。
	local($RemoteHost) = $ENV{ 'REMOTE_HOST' };

	# 引用ファイル、そのSubject
	local($ReplyArticleFile, $ReplyArticleSubject);

	# もし引用なら引用ファイル名を取得
	if ($cgi'tags{'id'} != 0) {
		$ReplyArticleFile = &GetArticleFileName($cgi'tags{'id'}, '');
		$ReplyArticleSubject = &GetSubject($cgi'tags{'id'}, $Board);
	} elsif ($cgi'tags{'file'} ne '') {
		$ReplyArticleFile = "../" . $cgi'tags{'file'};
		$ReplyArticleSubject = &GetSubjectFromFile($cgi'tags{'file'});
	}

	# エイリアスチェック
	$_ = $cgi'tags{'name'};
	if (/^#.*$/) {
		($cgi'tags{'name'}, $cgi'tags{'mail'}, $cgi'tags{'url'})
			= &GetUserInfo($_);
		if ($cgi'tags{'name'} eq "") {
			&MsgHeader($ERROR_MSG);
			print("<H2>$_はエイリアスに登録されていません</h2>");
			print("<p>戻ってもう一度。</p>");
			&MsgFooter;
			exit 0;
		}
	}

	# 空チェック
	&MyFatal(2, '') if ($cgi'tags{'subject'} eq "")
		|| ($cgi'tags{'article'} eq "")
		|| ($cgi'tags{'name'} eq "")
		|| ($cgi'tags{'mail'} eq "");

	# サブジェクトのタグチェック
	$_ = $cgi'tags{'subject'};
	&MyFatal(4, '') if (/</);

	# テンポラリファイルに書き出す。
	open(TMP, ">$TmpFile") || &MyFatal(1, $TmpFile);

	# まずヘッダ。
	printf(TMP "<strong>$H_SUBJECT</strong> %s<br>\n", $cgi'tags{'subject'});

	if ($cgi'tags{'url'} eq "http://" || $cgi'tags{'url'} eq "") {
		# URLがない場合
		printf(TMP "<strong>$H_FROM</strong> %s<br>\n", $cgi'tags{'name'});
	} else {
		# URLがある場合
		printf(TMP "<strong>$H_FROM</strong> <a href=\"%s\">%s</a><br>\n", $cgi'tags{'url'}, $cgi'tags{'name'});
	}

	printf(TMP "<strong>$H_MAIL</strong> <a href=\"mailto:%s\">&lt;%s&gt;</a><br>\n",
		$cgi'tags{'mail'}, $cgi'tags{'mail'});
#	print(TMP "<strong>$H_HOST</strong> $RemoteHost<br>\n");
# kinotropeではgethostbyaddrも使えないようなので、現状でホスト名は省略
	print(TMP "<strong>$H_DATE</strong> $InputDate<br>\n");

	# 引用の場合
	if ($cgi'tags{'id'} != 0) {
		printf(TMP "<strong>$H_REPLY</strong> [$BoardName: %d] <a href=\"$ReplyArticleFile\">$ReplyArticleSubject</a><br>\n", $cgi'tags{'id'});
	} elsif ($cgi'tags{'file'} ne '') {
		printf(TMP "<strong>$H_REPLY</strong> <a href=\"$ReplyArticleFile\">$ReplyArticleSubject</a><br>\n");
	}

	print(TMP "------------------------<br>\n");

	# article begin
	print(TMP "<!-- Article Begin -->\n");

	# TextType用前処理
	print(TMP "<pre>\n") if ($TextType eq $H_PRE);

	# 記事
	printf(TMP "%s\n", $cgi'tags{'article'});

	# TextType用後処理
	print(TMP "</pre>\n") if ($TextType eq $H_PRE);

	# article end
	print(TMP "<!-- Article End -->\n");
	print(TMP "<hr>\n");
	close TMP;

	# ファイル名を返す。
	return($TmpFile);
}


###
## 新たに投稿された記事の生成
#
sub MakeNewArticle {

	# テンポラリファイル名、Board、引用した記事のId
	local($TmpFile, $Board, $Id) = @_;

	# Board名称の取得
	local($BoardName) = &GetBoardInfo($Board);

	# 記事番号を収めるファイル
	local($ArticleNumFile) = $Board . "/" . $ARTICLE_NUM_FILE_NAME;

	# 新規の記事番号とファイル名
	local($ArticleId, $ArticleFile);

	# サブジェクト、入力日、名前
	local($Subject, $InputDate, $Name);

	# テンポラリファイルからSubject等を取り出す
	open(TMP, "$TmpFile") || &MyFatal(1, $TmpFile);
	while(<TMP>) {

		# subjectを取り出す。
		if (/^<strong>$H_SUBJECT<\/strong> (.*)<br>$/) {$Subject = $1; }
		# 日付を取り出す。
		if (/^<strong>$H_DATE<\/strong> (.*)<br>$/) {$InputDate = $1; }
		# 名前を取り出す。
		if (/^<strong>$H_FROM<\/strong> <a[^>]*>(.*)<\/a><br>$/) {
			$Name = $1;
		} elsif (/^<strong>$H_FROM<\/strong> (.*)<br>$/) {
			$Name = $1;
		}

	}
	close TMP;

	# ロックファイルを開く
	open(LOCK, "$LOCK_FILE")
		# 開けなきゃ作る
		|| (&MakeLockFile($LOCK_FILE) && open(LOCK, "$LOCK_FILE"))
		# 作れなきゃエラー
		|| &MyFatal(1, $LOCK_FILE);

	# ロックをかける
	&lock();

	# 記事番号を取得
	$ArticleId = &GetandAddArticleId($ArticleNumFile);

	# 正規のファイル名を取得
	$ArticleFile = &GetArticleFileName($ArticleId, $Board);

	# 正規のファイルにヘッダ部分を書き込む
	open(ART, ">$ArticleFile") || &MyFatal(1, $ArticleFile);

	# 記事ヘッダの作成
	printf(ART "<title>[$BoardName: %d] $Subject</title>\n", $ArticleId);
	print(ART "<body>\n");
	print(ART "<a href=\"$TITLE_FILE_NAME\">戻る</a> // ");
	printf(ART "<a href=\"%s\">前へ</a> // ",
		&GetArticleFileName(($ArticleId - 1), ''));
	printf(ART "<a href=\"%s\">次へ</a> // ",
		&GetArticleFileName(($ArticleId + 1), ''));
	print(ART "反応 ( <a href=\"$PROGRAM/$Board?c=q&id=$ArticleId\">引用有り</a> / ");
	print(ART "<a href=\"$PROGRAM/$Board?c=f&id=$ArticleId\">無し</a> ) // ");
	print(ART "<a href=\"$PROGRAM/$Board?c=t&id=$ArticleId\">まとめ読み</a>\n");
	print(ART "<hr>\n");

	# 記事ヘッダの始まり
	print(ART "<!-- Header Begin -->\n");

	# ボディの先頭にボード名と記事番号、題を入れる
	printf(ART "<strong>$H_SUBJECT</strong> [$BoardName: %d] $Subject<br>\n", $ArticleId);

	# テンポラリファイルからの記事のコピー
	open(TMP, "$TmpFile") || &MyFatal(1, $TmpFile);

	# Subject行は挿入済みなので1行飛ばす。
	$Dust = <TMP>;

	while(<TMP>) {
		print(ART $_)
	}
	close TMP;

	# テンポラリファイルの削除
	unlink("$TmpFile");

	# 記事フッタの作成
	print(ART "$H_FOLLOW\n<ol>\n");
	close ART;

	if ($Id != 0) {
		# フォローの場合

		# フォローされた記事にフォローされたことを書き込む。
		# フォローした記事ファイル名を直接渡さないのは、
		# 相対の起点が異なるため。
		&ArticleWasFollowed($Id, $Board, $ArticleId, $Subject, $Name);

		# タイトルファイルに投稿された記事を追加
		&AddTitleFollow($ArticleId, $Board, $Id, $Name, $Subject, $InputDate);
	} else {
		# フォローでなく、新規の場合

		# タイトルファイルに投稿された記事を追加
		&AddTitleNormal($ArticleId, $Board, $Name, $Subject, $InputDate);
	}

	# allファイルに投稿された記事を追加
	&AddAllFile($ArticleId, $Board, $Name, $Subject, $InputDate);

	# ロックを外す。
	&unlock();
	close LOCK;

}


###
## allリストに書き込む
#
sub AddAllFile {

	# 記事Id、Board, 名前、題、日付
	local($Id, $Board, $Name, $Subject, $InputDate) = @_;

	# 登録ファイル
	local($File) = $Board . "/" . $ALL_FILE_NAME;

	# 追加するファイルの名前
	local($ArticleFile) = &GetArticleFileName($Id, '');

	# Add to 'All' file
	open(ALL, ">>$File") || &MyFatal(1, $File);
	printf(ALL "<li><strong>$Id .</strong> <a href=\"%s\">$Subject</a> [$Name] $InputDate\n\n", $ArticleFile);
	close TITLE;
}
###
## タイトルリストに書き込む(新規)
#
sub AddTitleNormal {

	# 記事Id、Board, 名前、題、日付
	local($Id, $Board, $Name, $Subject, $InputDate) = @_;

	# 登録ファイル
	local($File) = $Board . "/" . $TITLE_FILE_NAME;

	# 追加するファイルの名前
	local($ArticleFile) = &GetArticleFileName($Id, '');

	# タイトルファイルに追加
	open(TITLE, ">>$File") || &MyFatal(1, $File);
	printf(TITLE "<li><strong>$Id .</strong> <a href=\"%s\">$Subject</a> [$Name] $InputDate\n\n", $ArticleFile);
	close TITLE;
}


###
## タイトルリストに書き込む(フォロー)
#
sub AddTitleFollow {

	# 記事Id、Board, フォロー記事Id、名前、題、日付
	local($Id, $Board, $Fid, $Name, $Subject, $InputDate) = @_;

	# 登録ファイル
	local($File) = $Board . "/" . $TITLE_FILE_NAME;

	# 追加するファイルの名前
	local($ArticleFile) = &GetArticleFileName($Id, '');

	# Followed Article File Name
	local($FollowedArticleFile) = &GetArticleFileName($Fid, '');

	# TmpFile
	local($TmpFile) = "$Board/$TTMP_FILE_NAME";

	# Follow Flag
	local($AddFlag, $Nest, $NextLine) = (0, 0, ''); 

	# タイトルファイルに追加
	open(TTMP, ">$TmpFile") || &MyFatal(1, $TmpFile);
	open(TITLE, "$File") || &MyFatal(1, $File);

	while(<TITLE>) {
		print(TTMP $_);

		if (/$FollowedArticleFile/) {
			&MyFatal(3, '') unless ($_ = <TITLE>);

			if (/^<ul>/) {
				$Nest = 1;
				do {
					print(TTMP $_);
					$_ = <TITLE>;
					$Nest++ if (/^<ul>/);
					$Nest-- if (/^<\/ul>/);
				} until ($Nest == 0);

				printf(TTMP "<li><strong>$Id .</strong> <a href=\"%s\">$Subject</a> [$Name] $InputDate\n\n", $ArticleFile);
				printf(TTMP $_);

			} else {

				print(TTMP "<ul>\n");
				printf(TTMP "<li><strong>$Id .</strong> <a href=\"%s\">$Subject</a> [$Name] $InputDate\n\n", $ArticleFile);
				print(TTMP "</ul>\n");

			}

			$AddFlag = "True";
		}
	}

	# If not found, followed Article must be old one.
	printf(TTMP "<li><strong>$Id .</strong> <a href=\"%s\">$Subject</a> [$Name] $InputDate\n\n", $ArticleFile) if (! $AddFlag);

	close TITLE;
	close TTMP;

	# Copy to Title File
	open(TITLE, ">$File") || &MyFatal(1, $File);
	open(TTMP, "$TmpFile") || &MyFatal(1, $TmpFile);
	while(<TTMP>) {
		print(TITLE $_);
	}
	close TTMP;
	close TITLE;

	# Chmod
	chmod($TITLE_FILE_PERMISSION, $File);

	# Delete Temporary File
	unlink("$TmpFile");
}


###
## フォローされた記事のフッタにフォローされたことを書き込む。
#
sub ArticleWasFollowed {

	# フォローされた記事のId、ボードの名称、
	# フォローした記事のId、サブジェクト、名前
	local($Id, $Board, $FollowArticleId, $Fsubject, $Fname) = @_;

	# フォローされた記事ファイル
	local($ArticleFile) = &GetArticleFileName($Id, $Board);

	# フォローした記事ファイル
	local($FollowArticleFile) = &GetArticleFileName($FollowArticleId, '');

	# 追加
	open(FART, ">>$ArticleFile") || &MyFatal(1, $ArticleFile);
	print(FART "<li><a href=\"$FollowArticleFile\">$Fsubject</a> ← $Fname さん\n");
	close FART;
}


###
## フォロー記事のIdの配列を取り出す。
#
sub GetFollowIdList {

	# IdとBoard
	local($Id, $Board) = @_;

	# 元ファイル
	local($File) = &GetArticleFileName($Id, $Board);

	# フォロー部分を判断するフラグ
	local($FollowFlag) = 0;

	# リスト
	local(@Result) = ();

	open(TMP, "$File") || &MyFatal(1, $File);
	while(<TMP>) {

		# フォロー部分開始の判定
		$QuoteFlag = 1 if (/^<!-- Article End -->$/);

		# フォローIdの取得
		if (($QuoteFlag == 1) &&
		(/^<li><a href=\"$ARTICLE_PREFIX\.([^\.]*)\.html\">/)) {
			push(@Result, $1);
		}
	}
	close TMP;

	return(@Result);
}


###
## 元記事の表示
#
sub ViewOriginalArticle {

	# IdとBoard
	local($Id, $Board) = @_;

	# 引用するファイル
	local($QuoteFile) = &GetArticleFileName($Id, $Board);

	# 引用部分を判断するフラグ
	local($QuoteFlag) = 0;

	open(TMP, "$QuoteFile") || &MyFatal(1, $QuoteFile);
	while(<TMP>) {

		# 引用終了の判定
		$QuoteFlag = 0 if (/^<!-- Article End -->$/);

		# 引用文字列の表示
		if ($QuoteFlag == 1) {
			print($_);
		}

		# 引用開始の判定
		$QuoteFlag = 1 if ((/^<!-- Header Begin -->$/) ||
					(/^<!-- Article Begin -->$/));

	}
	close TMP;

}


###
## 元記事の表示(ファイル)
#
sub ViewOriginalFile {

	# ファイル名
	local($File) = @_;

	# 引用部分を判断するフラグ
	local($QuoteFlag) = 0;

	open(TMP, "cat $File | /usr/local/bin/nkf -e |") || &MyFatal(1, $File);
	while(<TMP>) {

		# 引用終了の判定
		$QuoteFlag = 0 if (/^<!-- Article End -->$/);

		# 引用文字列の表示
		if ($QuoteFlag == 1) {
			print($_);
		}

		# 引用開始の判定
		$QuoteFlag = 1 if (/^<!-- Article Begin -->$/);

	}
	close TMP;

}


###
## 引用する
#
sub QuoteOriginalArticle {

	# IdとBoard
	local($Id, $Board) = @_;

	# 引用するファイル
	local($QuoteFile) = &GetArticleFileName($Id, $Board);

	# 引用部分を判断するフラグ
	local($QuoteFlag) = 0;

	open(TMP, "$QuoteFile") || &MyFatal(1, $QuoteFile);
	while(<TMP>) {

		# 引用終了の判定
		$QuoteFlag = 0 if (/^<!-- Article End -->$/);

		# 引用文字列の表示
		if ($QuoteFlag == 1) {
			s/&/&amp;/go;
			s/\"//go;
			s/<//go;
			s/>//go;
			print($DEFAULT_QMARK, $_);
		}

		# 引用開始の判定
		$QuoteFlag = 1 if (/^<!-- Article Begin -->$/);

	}
	close TMP;

}


###
## 引用する(ファイル)
#
sub QuoteOriginalFile {

	# ファイル名
	local($File) = @_;

	# 引用部分を判断するフラグ
	local($QuoteFlag) = 0;

	open(TMP, "cat $File | /usr/local/bin/nkf -e |") || &MyFatal(1, $File);
	while(<TMP>) {

		# 引用終了の判定
		$QuoteFlag = 0 if (/^<!-- Article End -->$/);

		# 引用文字列の表示
		if ($QuoteFlag == 1) {
			s/&/&amp;/go;
			s/\"//go;
			s/<//go;
			s/>//go;
			print($DEFAULT_QMARK, $_);
		}

		# 引用開始の判定
		$QuoteFlag = 1 if (/^<!-- Article Begin -->$/);

	}
	close TMP;

}


###
## 記事番号を取ってくる(番号は1増える)。
#
sub GetandAddArticleId {

	# ファイル名を取得
	local($ArticleNumFile) = @_;

	# 記事番号
	local($ArticleId) = 0;

	# 記事番号をファイルから読み込む。読めなかったら0のまま……のはず。
	open(AID, "$ArticleNumFile");
	while(<AID>) {
		chop;
		$ArticleId = $_;
	}
	close AID;

	# 1増やして書き込む。
	open(AID, ">$ArticleNumFile") || &MyFatal(1, $ArticleNumFile);
	print(AID $ArticleId + 1, "\n");
	close AID;

	# 新しい記事番号を返す。
	return($ArticleId + 1);
}


###
## 記事番号を取ってくる(番号は増えない)。
#
sub GetArticleId {

	# ファイル名を取得
	local($ArticleNumFile) = @_;

	# 記事番号
	local($ArticleId);

	open(AID, "$ArticleNumFile") || &MyFatal(1, $ArticleNumFile);
	while(<AID>) {
		chop;
		$ArticleId = $_;
	}
	close AID;

	# 記事番号を返す。
	return($ArticleId);
}


###
## ユーザエイリアスからユーザの名前、メール、URLを取ってくる。
#
sub GetUserInfo {

	# エイリアス名
	local($Alias) = @_;

	# 名前、メール、URL
	local($Name, $Mail, $URL);

	open(ALIAS, "$USER_ALIAS_FILE") || &MyFatal(1, $USER_ALIAS_FILE);
	while(<ALIAS>) {

		# マッチしなきゃ次へ。
		next unless (/^$Alias$/);

		$Name = <ALIAS>;
		chop($Name);
		$Mail = <ALIAS>;
		chop($Mail);
		$URL = <ALIAS>;
		chop($URL);

		# 配列にして返す
		return($Name, $Mail, $URL);
	}

	# ヒットせず
	return('', '', '');
}


###
## ボードエイリアスからボードエイリアス名を取ってくる。
#
sub GetBoardInfo {

	# エイリアス名
	local($Alias) = @_;

	# ボード名
	local($BoardName);

	open(ALIAS, "$BOARD_ALIAS_FILE") || &MyFatal(1, $BOARD_ALIAS_FILE);
	while(<ALIAS>) {

		chop;
		# マッチしなきゃ次へ。
		next unless (/^$Alias\t(.*)$/);
		$BoardName = $1;
		return($BoardName);
	}

	# ヒットせず
	return('');
}


###
## あるIdの記事からSubjectを取ってきて、先頭に「Re: 」を1つだけつけて返す。
#
sub GetReplySubject {

	# IdとBoard
	local($Id, $Board) = @_;

	# 取り出したSubject
	local($Subject) = '';

	# Subjectを取りだし、先頭に「Re: 」がくっついてたら取り除く。
	$_ = &GetSubject($Id, $Board);

	$Subject = (/^Re: (.*)/) ? $1 : $_;

	# 先頭に「Re: 」をくっつけて返す。
	return("Re: $Subject");

}


###
## あるファイルからTitleを取ってきて、先頭に「Re: 」をつけて返す。
#
sub GetReplySubjectFromFile {

	# ファイル
	local($File) = @_;

	# 取り出したSubject
	local($Title) = &GetSubjectFromFile($File);

	# 先頭に「Re: 」をくっつけて返す。
	return("Re: $Title");

}


###
## あるIdの記事からSubjectを取ってくる。
#
sub GetSubject {

	# IdとBoard
	local($Id, $Board) = @_;

	# Subjectを取り出すファイル
	local($ArticleFile) = &GetArticleFileName($Id, $Board);

	# 取り出したSubject
	local($Subject) = '';

	# 該当ファイルからSubject文字列を取り出す。
	open(TMP, "$ArticleFile") || &MyFatal(1, $ArticleFile);
	while(<TMP>) {
		if (/^<strong>$H_SUBJECT<\/strong> \[[^\]]*\] (.*)<br>$/) {
			$Subject = $1;
		}
	}
	close TMP;

	# 返す
	return($Subject);
}


###
## あるファイルからTitleを取ってくる
#
sub GetSubjectFromFile {

	# ファイル
	local($File) = @_;

	# 取り出したSubject
	local($Title) = '';

	open(TMP, "cat $File | /usr/local/bin/nkf -e |") || &MyFatal(1, $File);
	while(<TMP>) {

		if (/^<[Tt][Ii][Tt][Ll][Ee]>(.*)<\/[Tt][Ii][Tt][Ll][Ee]>$/) {
			$Title = $1;
		}
	}
	close TMP;

	# 返す。
	return($Title);

}


###
## ボード名称とIdからファイル名を作り出す。
#
sub GetArticleFileName {

	# IdとBoard
	local($Id, $Board) = @_;

	# Boardが空ならBoardディレクトリ内から相対、
	# 空でなければシステムから相対
	$Board
		? return("$Board/$ARTICLE_PREFIX.$Id.html")
		: return("$ARTICLE_PREFIX.$Id.html");
}


###
## ログファイルのロック関係
#
sub MakeLockFile {
	# ファイル名
	local($File) = @_;

	open(MAKELOCK, ">$File") || return(0);
	close MAKELOCK;
	return(1);
}

sub lock {
	# ロック
	flock(LOCK, $LOCK_EX);
	# おなじない
	seek(AID, 0, 2);
	seek(ART, 0, 2);
	seek(TITLE, 0, 2);
	seek(FART, 0, 2);
	seek(ALL, 0, 2);
}

sub unlock {
	# アンロック
	flock(LOCK, $LOCK_UN);
}


###
## 記事のヘッダの表示
#
sub MsgHeader {
	local($Message) = @_;

	&cgi'header;
	print("<title>$Message</title>", "\n");
	print("<body>\n");
	print("<h1>$Message</h1>\n");
	print("<hr>\n");
}


###
## 記事のフッタの表示
#
sub MsgFooter {
	print("<hr>\n");
	print("<address>\n");
	print("$ADDRESS\n");
	print("</address>\n");
	print("</body>");
}


###
## エラー表示
#
sub MyFatal {

	# エラー番号とエラー情報の取得
	local($MyFatalNo, $MyFatalInfo) = @_;
	#
	# 1 ... File関連
	# 2 ... 投稿の際の、必須項目の欠如
	#

	&MsgHeader($ERROR_MSG);

	if ($MyFatalNo == 1) {
		print("<p>File: $MyFatalInfoが存在しない、\n");
		print("あるいはpermissionの設定が間違っています。\n");
		print("お手数ですが、<a href=\"mailto:$Maint\"</a>$Maint</a>まで\n");
		print("上記ファイル名をお知らせ下さい。</p>\n");
	} elsif ($MyFatalNo == 2) {
		print("<p>題、記事、お名前、メールアドレス、\n");
		print("いずれかが入力されていません。\n");
		print("戻ってもう一度。</p>\n");
	} elsif ($MyFatalNo == 3) {
		print("<p>Title File is illegal.\n");
		print("お手数ですが、<a href=\"mailto:$Maint\"</a>$Maint</a>まで\n");
		print("お知らせ下さい。</p>\n");
	} elsif ($MyFatalNo == 4) {
		print("<p>ごめんなさい、題中にHTMLタグを入れることは\n");
		print("禁じられています。戻ってもう一度。</a>\n");
	} else {
		print("<p>エラー番号不定: お手数ですが、");
		print("このエラーが生じた状況を");
		print("<a href=\"mailto:$Maint\"</a>$Maint</a>までお知らせ下さい。</p>\n");
	}

	&MsgFooter;
	exit 0;
}


#/////////////////////////////////////////////////////////////////////


###
## cgi用パッケージ
#
package cgi;


###
## HTMLヘッダの生成
#
sub header {print "Content-type: text/html\n\n";}


###
## デコード
#
sub decode {
        local($args, $n_read, *terms, $tag, $value);

        $ENV{'REQUEST_METHOD'} eq "POST" ?
        ($n_read = read(STDIN, $args, $ENV{'CONTENT_LENGTH'})):
        ($args = $ENV{'QUERY_STRING'});

        @terms = split('&', $args);

        foreach (@terms) {
                ($tag, $value) = split(/=/, $_, 2);
                $value =~ tr/+/ /;
                $value =~ s/%([0-9A-Fa-f][0-9A-Fa-f])/pack("C", hex($1))/ge;
		$value =~ s/'/'\\''/go;
		$tags{$tag} = `echo -n '$value' | /usr/local/bin/nkf -e`;
		$tags{$tag} =~ s///ge;
        }
}


#/////////////////////////////////////////////////////////////////////
