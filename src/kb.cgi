#!/usr/local/bin/perl


# このファイルの変更は2箇所です．
#
# 1. ↑の先頭行で，Perlのパスを指定します．「#!」に続けて指定してください．

# 2. サーバがIISの場合，パッケージを展開・伸長してできた
#    kbディレクトリのフルパスを指定してください．
#    サーバがIISでなければ，この設定は不要です．
#
$IIS_TRANSLATED_PATH = '';
# $IIS_TRANSLATED_PATH = 'd:\inetpub\wwwroot\kb';

# 3. サーバが動いているマシンがWin95もしくはMacの場合，
#    $PCを1に設定してください．そうでない場合，この設定は不要です．
#
$PC = 0;	# for UNIX / WinNT
# $PC = 1;	# for Win95 / Mac


# 以下は書き換えの必要はありません．


######################################################################


# $Id: kb.cgi,v 5.16 1998-11-05 18:28:12 nakahiro Exp $

# KINOBOARDS: Kinoboards Is Network Opened BOARD System
# Copyright (C) 1995-98 NAKAMURA Hiroshi.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PRATICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

# This file implements main functions of KINOBOARDS.

# perlの設定
$[ = 0;				# zero origined
$| = 1;				# pipe flushed
$COLSEP = "\376";
# umaskは特に設定しない．混乱の元なので．．．
# umask( umask() | 070 );	# ユーザとnobodyが別グループの場合
# umask( umask() | 007 );	# ユーザとnobodyが同グループの場合

# 大域変数の定義
$HEADER_FILE = 'kb.ph';		# header file
$KB_VERSION = '1.0';		# version
$KB_RELEASE = '6.1';		# release
$MACPERL = ( $^O eq 'MacOS' );  # isMacPerl?

# ディレクトリ
$ICON_DIR = 'icons';				# アイコンディレクトリ
$UI_DIR = 'UI';					# UIディレクトリ

# ファイル
$BOARD_ALIAS_FILE = 'kinoboards';		# 掲示板DB
$CONF_FILE_NAME = 'kb.conf';			# 掲示板別configuratinファイル
$ARRIVEMAIL_FILE_NAME = 'kb.mail';		# 掲示板別新規メイル送信先DB
$BOARD_FILE_NAME = 'kb.board';			# タイトルリストヘッダDB
$DB_FILE_NAME = 'kb.db';			# 記事DB
$ARTICLE_NUM_FILE_NAME = 'kb.aid';		# 記事番号DB
$USER_ALIAS_FILE = 'kinousers';			# ユーザエイリアス用DB
$DEFAULT_ICONDEF = 'all.idef';			# アイコンDB
$LOCK_FILE = 'kb.lock';				# ロックファイル
$LOCK_FILE_B = '';				# 掲示板別ロックファイル
$LOGFILE = 'kb.klg';				# ログファイル
# Suffix
$TMPFILE_SUFFIX = 'tmp';			# DBテンポラリファイルのSuffix
$ICONDEF_POSTFIX = 'idef';			# アイコンDBファイルのSuffix

# ヘッダファイルの読み込み
require( $HEADER_FILE ) if ( -s "$HEADER_FILE" );

# 追加ヘッダファイルの読み込み
$PATH_TRANSLATED = $IIS_TRANSLATED_PATH || $ENV{'PATH_TRANSLATED'};
if ( $PATH_TRANSLATED ne '' )
{
    die( "cannot chdir to `$PATH_TRANSLATED'" )
	if ( !chdir( $PATH_TRANSLATED ));
    require( $HEADER_FILE ) if ( -s "$HEADER_FILE" );
    # 上でrequire済みの場合は読まない（Perlの言語仕様）
}

# インクルードファイルの読み込み
require( 'cgi.pl' );
require( 'kinologue.pl' );
$PROGNAME = $cgi'CGIPROG_NAME;
$PROGRAM = $cgi'PROGRAM;
$kinologue'SEV_THRESHOLD = $kinologue'SEV_WARN;

$cgi'SMTP_SERVER = $SMTP_SERVER;
$cgi'AF_INET = $AF_INET;
$cgi'SOCK_STREAM = $SOCK_STREAM;
$cgi'CHARSET = $CHARSET;
@cgi'TAG_ALLOWED = ( 'article', 'subject', 'key' );
$FF_LOG = ( $SYS_LOG == 1 ) ? $kinologue'FF_HTML : $kinologue'FF_PLAIN;
$SYS_F_MT = ($SYS_F_D || $SYS_F_AM || $SYS_F_MV);
if (( $cgi'SERVER_PORT != 80 ) && ( $SYS_PORTNO == 1 ))
{
    $SERVER_PORT_STRING = ":$cgi'SERVER_PORT";
}
else
{
    $SERVER_PORT_STRING = '';
}
$SCRIPT_URL = "http://$cgi'SERVER_NAME$SERVER_PORT_STRING$cgi'SCRIPT_NAME$cgi'PATH_INFO";
$BASE_URL = "http://$cgi'SERVER_NAME$SERVER_PORT_STRING$cgi'SYSDIR_NAME";
if ( $TIME_ZONE ) { $ENV{'TZ'} = $TIME_ZONE; }

# アイコンファイル相対URL
$ICON_BLIST = "$ICON_DIR/blist.gif";		# 掲示板一覧へ
$ICON_TLIST = "$ICON_DIR/tlist.gif";		# タイトル一覧へ
$ICON_PREV = "$ICON_DIR/prev.gif";		# 前の記事へ
$ICON_NEXT = "$ICON_DIR/next.gif";		# 次の記事へ
$ICON_WRITENEW = "$ICON_DIR/writenew.gif";	# 新規書き込み
$ICON_FOLLOW = "$ICON_DIR/follow.gif";		# リプライ
$ICON_QUOTE = "$ICON_DIR/quote.gif";		# 引用してリプライ
$ICON_THREAD = "$ICON_DIR/thread.gif";		# まとめ読み
$ICON_HELP = "$ICON_DIR/help.gif";		# ヘルプ
$ICON_DELETE = "$ICON_DIR/delete.gif";		# 削除
$ICON_SUPERSEDE = "$ICON_DIR/supersede.gif";	# 訂正
$ICON_NEW = "$ICON_DIR/listnew.gif";		# 新着

# シグナルハンドラ
$SIG{'QUIT'} = 'IGNORE';
$SIG{'INT'} = $SIG{'HUP'} = $SIG{'TERM'} = $SIG{'TSTP'} = 'DoKill';
sub DoKill
{
    local( $sig ) = @_;
    if ( !$PC )
    {
	&cgi'unlock( $LOCK_FILE );
	&cgi'unlock( $LOCK_FILE_B ) if $LOCK_FILE_B;
    }
    &KbLog( $kinologue'SEV_WARN, "Caught a SIG$sig - shutting down..." );
    exit( 1 );
}


######################################################################


###
## MAIN - メインブロック
#
# - SYNOPSIS
#	kb.cgi
#
# - ARGS
#	なし
#
# - DESCRIPTION
#	起動時に一度だけ参照される．
#	引き数はないが，環境変数QUERY_STRINGとREQUEST_METHOD，
#	もしくは標準入力経由で値を渡さないと，正しく動作しない．
#
# - RETURN
#	なし
#
MAIN:
{
    local( $boardConfFileP, $c, $com );

    &cgi'Decode();
    $c = $cgi'TAGS{'c'};
    $com = $cgi'TAGS{'com'};
    $BOARD = $cgi'TAGS{'b'};
    if ( $c eq '' ) { $c = 'v'; $BOARD = 'test'; }
    if ( $BOARD )
    {
	( $BOARDNAME, $boardConfFileP ) = &GetBoardInfo( $BOARD );
	$LOCK_FILE_B = $LOCK_FILE . ".$BOARD";
    }

    # 掲示板固有セッティングを読み込む
    if ( $boardConfFileP )
    {
	local( $boardConfFile ) = &GetPath( $BOARD, $CONF_FILE_NAME );
	eval( "require( \"$boardConfFile\" );" ) || &Fatal( 1, $boardConfFile );
    }
    if ( $BOARDLIST_URL eq '-' ) { $BOARDLIST_URL = "$PROGRAM?c=bl"; }

    if ( $c eq 'e' )
    {
	### ShowArticle - 単一記事の表示
	require( &GetPath( $UI_DIR, 'ShowArticle.pl' ));
	last;
    }
    elsif ( $SYS_F_T && ( $c eq 't' ))
    {
	### ThreadArticle - フォロー記事を全て表示．
	require( &GetPath( $UI_DIR, 'ThreadArticle.pl' ));
	last;
    }

    if ( $SYS_F_N )
    {
	if ( $c eq 'n' )
	{
	    ### Entry - 書き込み画面の表示
	    $gVarQuoteFlag = 0;
	    require( &GetPath( $UI_DIR, 'Entry.pl' ));
	    last;
	}
	elsif ( $c eq 'f' )
	{
	    $gVarQuoteFlag = 1;
	    require( &GetPath( $UI_DIR, 'Entry.pl' ));
	    last;
	}
	elsif ( $c eq 'q' )
	{
	    $gVarQuoteFlag = 2;
	    require( &GetPath( $UI_DIR, 'Entry.pl' ));
	    last;
	}
	elsif (( $c eq 'p' ) && ( $com ne 'x' ))
	{
	    ### Preview - プレビュー画面の表示
	    require( &GetPath( $UI_DIR, 'Preview.pl' ));
	    last;
	}
	elsif (( $c eq 'x' ) || (( $c eq 'p' ) && ( $com eq 'x' )))
	{
	    ### Thanks - 登録後画面の表示
	    require( &GetPath( $UI_DIR, 'Thanks.pl' ));
	    last;
	}
    }

    if ( $c eq 'v' )
    {
	### ViewTitle - スレッド別表示
	$gVarComType = 0;
	require( &GetPath( $UI_DIR, 'ViewTitle.pl' ));
	last;
    }
    elsif ( $SYS_F_R && ( $c eq 'r' ))
    {
	### SortArticle - 日付順にソート
	require( &GetPath( $UI_DIR, 'SortArticle.pl' ));
	last;
    }
    elsif ( $SYS_F_L && ( $c eq 'l' ))
    {
	### NewArticle - 新しい記事をまとめて表示
	require( &GetPath( $UI_DIR, 'NewArticle.pl' ));
	last;
    }
    elsif ( $SYS_F_S && ( $c eq 's' ))
    {
	### SearchArticle - 記事の検索(表示画面の作成)
	require( &GetPath( $UI_DIR, 'SearchArticle.pl' ));
	last;
    }
    elsif ( $c eq 'i' )
    {
	### ShowIcon - アイコン表示画面
	require( &GetPath( $UI_DIR, 'ShowIcon.pl' ));
	last;
    }

    if ( $SYS_ALIAS )
    {
	if ( $c eq 'an' )
	{
	    ### AliasNew - エイリアスの登録と変更画面の表示
	    require( &GetPath( $UI_DIR, 'AliasNew.pl' ));
	    last;
	}
	elsif ( $c eq 'am' )
	{
	    ### AliasMod - ユーザエイリアスの登録/変更
	    require( &GetPath( $UI_DIR, 'AliasMod.pl' ));
	    last;
	}
	elsif ( $c eq 'ad' )
	{
	    ### AliasDel - ユーザエイリアスの削除
	    require( &GetPath( $UI_DIR, 'AliasDel.pl' ));
	    last;
	}
	elsif ( $c eq 'as' )
	{
	    ### AliasShow - ユーザエイリアス参照画面の表示
	    require( &GetPath( $UI_DIR, 'AliasShow.pl' ));
	    last;
	}
    }

    if ( $SYS_F_B && ( $c eq 'bl' ))
    {
	### BoardList - 掲示板一覧の表示
	require( &GetPath( $UI_DIR, 'BoardList.pl' ));
	last;
    }

    # 以下は管理用
    if ( $SYS_F_MT )
    {
	if ( $c eq 'mtr' )
	{
	    $gVarComType = 1;
	    require( &GetPath( $UI_DIR, 'View.pl' ));
	    last;
	}
	elsif ( $c eq 'vm' )
	{
	    $gVarComType = 1;
	    require( &GetPath( $UI_DIR, 'ViewTitle.pl' ));
	    last;
	}
    }

    if ( $SYS_F_MV )
    {
	if  ( $c eq 'ct' )
	{
	    $gVarComType = 2;
	    require( &GetPath( $UI_DIR, 'ViewTitle.pl' ));
	    last;
	}
	elsif ( $c eq 'ce' )
	{
	    $gVarComType = 3;
	    require( &GetPath( $UI_DIR, 'ViewTitle.pl' ));
	    last;
	}
	elsif ( $c eq 'mvt' )
	{
	    $gVarComType = 4;
	    require( &GetPath( $UI_DIR, 'ViewTitle.pl' ));
	    last;
	}
	elsif ( $c eq 'mve' )
	{
	    $gVarComType = 5;
	    require( &GetPath( $UI_DIR, 'ViewTitle.pl' ));
	    last;
	}
    }

    if ( $SYS_F_D )
    {
	if ( $c eq 'dp' )
	{
	    ### DeletePreview - 削除記事の確認
	    require( &GetPath( $UI_DIR, 'DeletePreview.pl' ));
	    last;
	}
	elsif ( $c eq 'de' )
	{
	    ### DeleteExec - 記事の削除
	    $gVarThreadFlag = 0;
	    require( &GetPath( $UI_DIR, 'DeleteExec.pl' ));
	    last;
	}
	elsif ( $c eq 'det' ) {
	    $gVarThreadFlag = 1;
	    require( &GetPath( $UI_DIR, 'DeleteExec.pl' ));
	    last;
	}
    }

    if ( $SYS_F_AM )
    {
	if ( $c eq 'mp' )
	{
	    ### ArriveMailEntry - メイル自動配信先の指定
	    require( &GetPath( $UI_DIR, 'ArriveMailEntry.pl' ));
	    last;
	}
	elsif ( $c eq 'me' )
	{
	    ### ArriveMailExec - メイル自動配信先の設定
	    require( &GetPath( $UI_DIR, 'ArriveMailExec.pl' ));
	    last;
	}
    }

    # どのコマンドでもない．エラー．
    &Fatal( 99, '' );
}

exit( 0 );


######################################################################
# ユーザインタフェイスインプリメンテーション(個別)
#
# UIディレクトリに収められているUIの実装モジュールをrequireする．
# 不要なプログラムをコンパイルしないようにするため．
# 各関数のリファレンスは，UIディレクトリ内の各ファイルを参照のこと．


### Fatal - エラー表示
sub Fatal
{
    ( $gVarFatalNo, $gVarFatalInfo ) = @_;
    require( &GetPath( $UI_DIR, 'Fatal.pl' ));
}

### ArriveMail - 記事が到着したことをメイル
sub ArriveMail
{
    ( $gName, $gEmail, $gSubject, $gIcon, $gId, @gTo ) = @_;
    require( &GetPath($UI_DIR, 'ArriveMail.pl' ));
}

### FollowMail - 反応があったことをメイル
sub FollowMail
{
    ( $gName, $gEmail, $gDate, $gSubject, $gIcon, $gId, $gFname, $gFemail, $gFsubject, $gFicon, $gFid, @gTo ) = @_;
    require( &GetPath( $UI_DIR, 'FollowMail.pl' ));
}


######################################################################
# ユーザインタフェイスインプリメンテーション(共通部)


###
## ViewOriginalArticle - 元記事の表示
#
# - SYNOPSIS
#	ViewOriginalArticle($Id, $CommandFlag, $OriginalFlag);
#
# - ARGS
#	$Id			記事ID
#	$CommandFlag		コマンドを表示するか否か(表示する=1)
#	$OriginalFlag		その記事中に，(あれば)元記事へのリンクを
#				表示するか否か(表示する=1)
#
# - DESCRIPTION
#	元記事を表示する．
#
# - RETURN
#	なし
#
sub ViewOriginalArticle
{
    local( $Id, $CommandFlag, $OriginalFlag ) = @_;

    local( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url ) = &GetArticlesInfo( $Id );

    local( $Num );
    foreach ( 0 .. $#DB_ID ) { $Num = $_, last if ( $DB_ID[$_] eq $Id ); }
    local( $PrevId ) = $DB_ID[$Num - 1] if ( $Num > 0 );
    local( $NextId ) = $DB_ID[$Num + 1];

    if ( $CommandFlag && $SYS_COMMAND )
    {
	# コマンド表示
	if ( $SYS_COMICON == 1 )
	{
	    $DlmtS = "";
	    $DlmtL = " // ";
	}
	elsif ( $SYS_COMICON == 2 )
	{
	    $DlmtS = " | ";
	    $DlmtL = "";
	}
	else
	{
	    $DlmtS = " | ";
	    $DlmtL = "";
	}

	&cgiprint'Cache( "<p>\n" );

	&cgiprint'Cache( &TagA( $BOARDLIST_URL, &TagComImg( $ICON_BLIST, $H_BACKBOARD, $SYS_COMICON )), "\n" ) if $SYS_F_B;

	&cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=v&num=$DEF_TITLE_NUM", &TagComImg( $ICON_TLIST, $H_BACKTITLEREPLY, $SYS_COMICON )), "\n" );
	
	local( $TagTmp ) = &TagComImg( $ICON_PREV, $H_PREVARTICLE, $SYS_COMICON );
	if ( $PrevId ne '' )
	{
	    &cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=e&id=$PrevId", $TagTmp ), "\n" );
	}
	else
	{
	    &cgiprint'Cache( $DlmtS, $TagTmp, "\n" );
	}
	
	$TagTmp = &TagComImg( $ICON_NEXT, $H_NEXTARTICLE, $SYS_COMICON );
	if ( $NextId ne '' )
	{
	    &cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=e&id=$NextId", $TagTmp ), "\n" );
	}
	else
	{
	    &cgiprint'Cache( $DlmtS, $TagTmp, "\n" );
	}
	
	if ( $SYS_F_T )
	{
	    $TagTmp = &TagComImg( $ICON_THREAD, $H_READREPLYALL, $SYS_COMICON );
	    if ( $Aids ne '' )
	    {
		&cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=t&id=$Id", $TagTmp ), "\n" );
	    }
	    else
	    {
		&cgiprint'Cache( $DlmtS, $TagTmp, "\n");
	    }
	}

	&cgiprint'Cache( $DlmtL, "\n" ) if $DlmtL;

	if ( $SYS_F_N )
	{
	    &cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=n", &TagComImg( $ICON_WRITENEW, $H_POSTNEWARTICLE, $SYS_COMICON )), "\n" );
	    &cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=f&id=$Id", &TagComImg( $ICON_FOLLOW, $H_REPLYTHISARTICLE, $SYS_COMICON )), "\n" );
	    &cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=q&id=$Id", &TagComImg( $ICON_QUOTE, $H_REPLYTHISARTICLEQUOTE, $SYS_COMICON )), "\n" );
	}

	&cgiprint'Cache( $DlmtL, "\n" ) if $DlmtL;

	&cgiprint'Cache( $DlmtS, &TagA( "$PROGRAM?b=$BOARD&c=i&type=article", &TagComImg( $ICON_HELP, "ヘルプ", $SYS_COMICON )), "\n")
	    if ( $SYS_COMICON == 1 );

	&cgiprint'Cache( "</p>\n" );
    }

    &cgiprint'Cache( "<p>\n" );

    # 記事番号，題
    &cgiprint'Cache( "<strong>$H_SUBJECT</strong>: <strong>$Id .</strong> " );
    if (( $Icon eq $H_NOICON ) || ( $Icon eq '' ))
    {
	&cgiprint'Cache( $Subject );
    }
    else
    {
	&cgiprint'Cache( &TagMsgImg( &GetIconUrlFromTitle( $Icon, $BOARD ),
				    $Icon ), $Subject );
    }

    # お名前
    if ( $Url eq '' )
    {
	&cgiprint'Cache( "<br>\n<strong>$H_FROM</strong>: ", $Name );
    }
    else
    {
	&cgiprint'Cache( "<br>\n<strong>$H_FROM</strong>: ",
			&TagA( $Url, $Name ));
    }

    # メイル
    &cgiprint'Cache( " ", &TagA( "mailto:$Email" , "&lt;$Email&gt;" )) if $Email;

    # マシン
    &cgiprint'Cache( "<br>\n<strong>$H_HOST</strong>: $RemoteHost" )
	if $SYS_SHOWHOST;

    # 投稿日
    &cgiprint'Cache( "<br>\n<strong>$H_DATE</strong>: ",
		    &GetDateTimeFormatFromUtc( $Date ));

    # 反応元(引用の場合)
    &ShowLinksToFollowedArticle( split( /,/, $Fid ))
	if ( $OriginalFlag && ( $Fid ne '' ));

    # 切れ目
    &cgiprint'Cache( "</p>\n$H_LINE\n" );

    # 記事の中身
    local( @ArticleBody );
    &GetArticleBody( $Id, $BOARD, *ArticleBody );
    &cgiprint'Cache( @ArticleBody );
}


###
## ThreadArticleMain - フォロー記事を全て表示．
#
# - SYNOPSIS
#	ThreadArticle($SubjectOnly, $Head, @Tail);
#
# - ARGS
#	$SubjectOnly	タイトルのみを表示するのか，
#			あるいは記事本文も表示するのか．
#	$Head		木構造のヘッドノード
#	@Tail		木構造の娘ノード群
#
# - DESCRIPTION
#	ある記事と，その記事へのリプライ記事をまとめて表示する．
#	元の木構造は，
#		( a ( b ( c d ) ) ( e ) ( f ( g ) ) )
#	のようなリストである．
#	詳細は&GetFollowIdTreeのインプリメント部分を参照のこと．
#
# - RETURN
#	なし
#
sub ThreadArticleMain
{
    local( $SubjectOnly, $Head, @Tail ) = @_;

    # 記事概要か，記事そのものか．
    if ( $SubjectOnly )
    {
	if ( $Head eq '(' )
	{
	    &cgiprint'Cache( "<ul>\n" );
	}
	elsif ( $Head eq ')' )
        {
	    &cgiprint'Cache( "</ul>\n" );
	}
	else
	{
	    local( $dFid, $dAids, $dDate, $dSubject, $dIcon, $dRemoteHost, $dName ) = &GetArticlesInfo( $Head );
	    &cgiprint'Cache( "<li>", &GetFormattedTitle( $Head, $BOARD, $dAids, $dIcon, $dSubject, $dName, $dDate ), "\n" );
	}
    }
    elsif (( $Head ne '(' ) && ( $Head ne ')' ))
    {
	# 元記事の表示(コマンド付き, 元記事なし)
	&cgiprint'Cache( "$H_HR\n" );
	&ViewOriginalArticle( $Head, 1, 0 );
    }

    # tail recuresive.
    &ThreadArticleMain( $SubjectOnly, @Tail ) if @Tail;
}


###
## QuoteOriginalArticle - 引用する(引用符あり)
#
# - SYNOPSIS
#	QuoteOriginalArticle($Id);
#
# - ARGS
#	$Id		記事ID
#
# - DESCRIPTION
#	記事を引用して表示する
#
# - RETURN
#	なし
#
sub QuoteOriginalArticle
{
    local( $Id, *msg ) = @_;

    local( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name, $pFid, $pAids, $pDate, $pSubject, $pIcon, $pRemoteHost, $pName, $QMark, $line, @ArticleBody );

    # 元記事情報の取得
    ( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name ) = &GetArticlesInfo( $Id );

    # 元記事のさらに元記事情報
    if ( $Fid )
    {
	$Fid =~ s/,.*$//o;
	( $pFid, $pAids, $pDate, $pSubject, $pIcon, $pRemoteHost, $pName ) = &GetArticlesInfo( $Fid );
    }

    # 引用
    &GetArticleBody( $Id, $BOARD, *ArticleBody );
    foreach $line ( @ArticleBody )
    {
	&TAGEncode( *line );

	$QMark = $DEFAULT_QMARK;
	$QMark = $Name . $QMark if $SYS_QUOTENAME;

	# 元文のうち，引用部分には，新たに引用文字列を重ねない
	# 空行にも要らない
	$QMark = '' if (( $line =~ /^$/o ) || ( $line =~ /^$pName\s*$DEFAULT_QMARK/ ));

	# 引用文字列の表示
	$msg .= "$QMark$line";
    }
}


###
## QuoteOriginalArticleWithoutQMark - 引用する(引用符なし)
#
# - SYNOPSIS
#	QuoteOriginalArticleWithoutQMark($Id);
#
# - ARGS
#	$Id		記事ID
#
# - DESCRIPTION
#	記事を引用して表示する
#
# - RETURN
#	なし
#
sub QuoteOriginalArticleWithoutQMark
{
    local( $Id, *msg ) = @_;

    local( @ArticleBody, $line );
    &GetArticleBody( $Id, $BOARD, *ArticleBody );
    foreach $line ( @ArticleBody )
    {
	&TAGEncode( *line );
	$msg .= $line;
    }
}


###
## BoardHeader - 掲示板ヘッダの表示
#
# - SYNOPSIS
#	BoardHeader($Type);
#
# - ARGS
#	$Type	掲示板ヘッダのタイプ
#			'normal' ... 通常
#			'maint' .... 管理用
#
# - DESCRIPTION
#	掲示板のヘッダを表示する．
#
# - RETURN
#	なし
#
sub BoardHeader
{
    local( $Type ) = @_;

    local( @BoardHeader );
    &GetBoardHeader( $BOARD, *BoardHeader );
    &cgiprint'Cache( @BoardHeader );

    if ( $SYS_F_MT && ( $Type eq 'normal' ))
    {
	&cgiprint'Cache( "<p>\n<ul>\n" );
	&cgiprint'Cache( "<li>", &TagA( "$PROGRAM?c=vm&b=$BOARD&num=$DEF_TITLE_NUM", "管理者用のタイトル一覧へ" ), "\n</ul>\n</p>\n" );
    }
    elsif ( $Type eq 'maint' )
    {
	&cgiprint'Cache( "<p>\n<ul>\n" );
	&cgiprint'Cache( "<li>", &TagA( "$PROGRAM?c=mp&b=$BOARD", "自動メイル配信先を設定する" ), "\n" ) if $SYS_F_AM;
	&cgiprint'Cache( "<li>", &TagA( "$PROGRAM?c=v&b=$BOARD&num=$DEF_TITLE_NUM", "通常のタイトル一覧へ" ), "\n</ul>\n</p>\n" );
    }
}


###
## ShowLinksToFollowedArticle - 元記事情報の表示
#
# - SYNOPSIS
#	ShowLinksToFollowedArticle(@IdList);
#
# - ARGS
#	@IdList		リプライ記事IDのリスト(古いリプライほど末尾にくる)
#
# - DESCRIPTION
#	元記事情報を表示する．
#
# - RETURN
#	なし
#
sub ShowLinksToFollowedArticle
{
    local( @IdList ) = @_;

    local( $Id, $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name );

    # オリジナル記事
    if ( $#IdList > 0 )
    {
	$Id = $IdList[$#IdList];
	( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name ) = &GetArticlesInfo( $Id );
	&cgiprint'Cache( "<br>\n<strong>$H_ORIG_TOP:</strong> ", &GetFormattedTitle( $Id, $BOARD, $Aids, $Icon, $Subject, $Name, $Date ));
    }

    # 元記事
    $Id = $IdList[0];
    ( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name ) = &GetArticlesInfo( $Id );
    &cgiprint'Cache( "<br>\n<strong>$H_ORIG:</strong> ", &GetFormattedTitle( $Id, $BOARD, $Aids, $Icon, $Subject, $Name, $Date ));
}


###
## PrintButtonToTitleList - タイトル一覧へ戻るボタンの表示
#
# - SYNOPSIS
#	PrintButtonToTitleList($Board);
#
# - ARGS
#	$Board		掲示板ID
#
# - DESCRIPTION
#	タイトル一覧に戻るためのボタンを表示する
#
# - RETURN
#	なし
#
sub PrintButtonToTitleList
{
    local( $Board ) = @_;

    local( %tags ) = ( 'b', $Board, 'c', 'v', 'num', $DEF_TITLE_NUM );
    local( $str );
    &TagForm( *str, *tags, "$H_BACKTITLEREPLY", '', '' );
    &cgiprint'Cache( $str );

    if ( $H_BACKTITLEDATE )
    {
	%tags = ( 'b', $Board, 'c', 'r', 'num', $DEF_TITLE_NUM );
	&TagForm( *str, *tags, "$H_BACKTITLEDATE", '', '' );
	&cgiprint'Cache( $str );
    }
}


###
## PrintButtonToBoardList - 掲示板一覧へ戻るボタンの表示
#
# - SYNOPSIS
#	PrintButtonToBoardList;
#
# - ARGS
#	なし
#
# - DESCRIPTION
#	掲示板一覧に戻るためのボタンを表示する
#
# - RETURN
#	なし
#
sub PrintButtonToBoardList
{
    if ( $BOARDLIST_URL =~ /$PROGRAM/ )
    {
	local( %tags ) = ( 'c', 'bl' );
	local( $str );
	&TagForm( *str, *tags, $H_BACKBOARD, '', '' );
	&cgiprint'Cache( $str );
    }
    else
    {
	&cgiprint'Cache( "<p><a href=\"$BOARDLIST_URL\">$H_BACKBOARD</a></p>\n" );
    }
}


###
## MsgHeader - HTML文書のヘッダ部分の表示
#
# - SYNOPSIS
#	MsgHeader($Title, $Message, $LastModified);
#
# - ARGS
#	$Title		HTML文書のTITLE(titleタグに入れるので，今のところ，
#			US-ASCIIのみしておいたほうが無難)
#	$Message	HTML文書のタイトル(本文内に表示する文字列)
#	$LastModified	最終更新時間
#
# - DESCRIPTION
#	HTML文書のヘッダ部分を表示する．
#
# - RETURN
#	なし
#
sub MsgHeader
{
    local( $Title, $Message, $LastModified ) = @_;
    
    if (( $SYS_ALIAS == 3 ) && ( $cgi'TAGS{'cookies'} eq 'on' ))
    {
	local( @cookieStr ) = ( "kb10info=" . join( $COLSEP, $cgi'TAGS{ 'name' }, $cgi'TAGS{ 'mail' }, $cgi'TAGS{ 'url' } ));
	local( $cookieExpire );
	if ( $SYS_COOKIE_EXPIRE == 1 )
	{
	    $cookieExpire = 'Thursday, 31-Dec-2029 23:59:59 GMT';
	}
	elsif ( $SYS_COOKIE_EXPIRE == 2 )
	{
	    $cookieExpire = $^T + ( $SYS_COOKIE_VALUE * 86400 );
	    # 86400 = 24 * 60 * 60
	}
	elsif ( $SYS_COOKIE_EXPIRE == 3 )
	{
	    $cookieExpire = $SYS_COOKIE_VALUE;
	}
	else
	{
	    $cookieExpire = '';
	}
	&cgi'Header( 0, 0, 1, *cookieStr, $cookieExpire );
    }
    else
    {
	# Last-Modifiedは空．Cookiesも空．
	&cgi'Header( 0, 0, 0, 0, 0 );
    }

    &cgiprint'Init();
    &cgiprint'Cache(<<__EOF__);
<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML i18n//EN">
<html>
<head>
<base href="$BASE_URL">
<title>$Title - $BOARDNAME - $SYSTEM_NAME</title>
<LINK REV=MADE HREF="mailto:$MAINT">
</head>
__EOF__

    &cgiprint'Cache( "<body" );
    if ( $SYS_NETSCAPE_EXTENSION )
    {
	&cgiprint'Cache(" background=\"$BG_IMG\"") if $BG_IMG;
	&cgiprint'Cache(" bgcolor=\"$BG_COLOR\"") if $BG_COLOR;
	&cgiprint'Cache(" TEXT=\"$TEXT_COLOR\"") if $TEXT_COLOR;
	&cgiprint'Cache(" LINK=\"$LINK_COLOR\"") if $LINK_COLOR;
	&cgiprint'Cache(" ALINK=\"$ALINK_COLOR\"") if $ALINK_COLOR;
	&cgiprint'Cache(" VLINK=\"$VLINK_COLOR\"") if $VLINK_COLOR;
    }
    &cgiprint'Cache( ">\n" );

    &cgiprint'Cache(<<__EOF__);
<h1>$Message</h1>

<p>[
__EOF__

    &cgiprint'Cache( "$H_BOARD: $BOARDNAME // \n" ) if ( $BOARD && $BOARDNAME );
    &cgiprint'Cache( "時刻: ", &GetDateTimeFormatFromUtc( $^T ));
    &cgiprint'Cache(<<__EOF__);
]</p>

$H_HR

__EOF__
}


###
## MsgFooter - HTML文書のフッタ部分の表示
#
# - SYNOPSIS
#	MsgFooter;
#
# - ARGS
#	なし
#
# - DESCRIPTION
#	HTML文書のフッタ部分を表示する．
#
# - RETURN
#	なし
#
sub MsgFooter
{
    # ↓これを変更するのも「自由」です．消しても全く問題ありません．
    local( $addr ) = "Maintenance: " . &TagA( "mailto:$MAINT", $MAINT_NAME ) . "<br>" . &TagA( "http://www.kinotrope.co.jp/~nakahiro/kb10.shtml", "KINOBOARDS/$KB_VERSION R$KB_RELEASE" ) . ": Copyright (C) 1995-98 " . &TagA( "http://www.kinotrope.co.jp/~nakahiro/", "NAKAMURA Hiroshi" ) . ".";
    # ただし，「俺が全部作ったんだ!」とか書くと，なひの権利を侵害して，
    # やっぱりGPL2に違反することになっちゃうので気をつけてね．(^_^;

    &cgiprint'Cache(<<__EOF__);
$H_HR
<address>
$addr
</address>
</body>
</html>
__EOF__

    &cgiprint'Flush();
}


###
## GetFormattedTitle - タイトルリストのフォーマット
#
# - SYNOPSIS
#	GetFormattedTitle($Id, $Board, $Aids, $Icon, $Title, $Name, $Date);
#
# - ARGS
#	$Id		記事ID
#	$Board		掲示板ID
#	$Aids		リプライ記事があるか否か
#	$Icon		記事アイコンID
#	$Title		記事のSubject
#	$Name		記事の投稿者名
#	$Date		記事の投稿日付(UTC)
#
# - DESCRIPTION
#	ある記事をタイトルリスト表示用にフォーマットする．
#
# - RETURN
#	フォーマットした文字列
#
sub GetFormattedTitle
{
    local( $Id, $Board, $Aids, $Icon, $Title, $Name, $Date ) = @_;

    local( $String, $InputDate, $IdStr, $Link, $Thread, $NewIcon );

    $InputDate = &GetDateTimeFormatFromUtc(( $Date || &GetModifiedTime( $Id, $Board )));
    # タイトルがついてなかったら，Idをそのままタイトルにする．
    $Title = $Title || $Id;

    $IdStr = "<strong>$Id.</strong> ";
    $Link = &TagA( "$PROGRAM?b=$Board&c=e&id=$Id", $Title );
    $Thread = (($SYS_F_T && $Aids) ? " " . &TagA( "$PROGRAM?b=$Board&c=t&id=$Id", $H_THREAD ) : '');
    $NewIcon = " " . &TagMsgImg( $ICON_NEW, $H_NEWARTICLE ) if $DB_NEW{$Id};

    if (( $Icon eq $H_NOICON ) || ($Icon eq '' ))
    {
	$String = "$IdStr$Link$Thread [" . ( $Name || $MAINT_NAME ) . "] $InputDate$NewIcon";
    }
    else
    {
#	$String = $IdStr . &TagMsgImg( &GetIconUrlFromTitle( $Icon, $Board ), "$Icon " ) . "$Link [" . ( $Name || $MAINT_NAME ) . "] $InputDate$NewIcon";
	$String = $IdStr . &TagMsgImg( &GetIconUrlFromTitle( $Icon, $Board ), "$Icon " ) . "$Link$Thread [" . ( $Name || $MAINT_NAME ) . "] $InputDate$NewIcon";
    }
    $String;
}


###
## TagComImg - コマンドアイコン用イメージタグのフォーマット
#
# - SYNOPSIS
#	TagComImg( $src, $alt, $textP );
#
# - ARGS
#	$src		ソースイメージのURL
#	$alt		altタグ用の文字列
#	$type		表示タイプ
#				1 ... アイコンの後ろに文字列を追加しない
#				2 ... アイコンの後ろに文字列を追加しない
#				0/others ... アイコンなしでテキストだけ
#
# - DESCRIPTION
#	イメージを表示用タグにフォーマットする．
#
# - RETURN
#	フォーマットした文字列
#
sub TagComImg
{
    local( $src, $alt, $type ) = @_;
    if ( $type == 1 )
    {
	"<img src=\"$src\" alt=\"$alt\" width=\"$COMICON_WIDTH\" height=\"$COMICON_HEIGHT\" BORDER=\"0\">";
    }
    elsif ( $type == 2 )
    {
	"<img src=\"$src\" alt=\"$alt\" width=\"$COMICON_WIDTH\" height=\"$COMICON_HEIGHT\" BORDER=\"0\">$alt";
    }
    else
    {
	$alt;
    }
}


###
## TagMsgImg - 記事アイコン用イメージタグのフォーマット
#
# - SYNOPSIS
#	TagMsgImg( $src, $alt );
#
# - ARGS
#	$src		ソースイメージのURL
#	$alt		altタグ用の文字列
#
# - DESCRIPTION
#	イメージを表示用タグにフォーマットする．
#
# - RETURN
#	フォーマットした文字列
#
sub TagMsgImg
{
    local( $src, $alt ) = @_;
    "<img src=\"$src\" alt=\"$alt\" width=\"$MSGICON_WIDTH\" height=\"$MSGICON_HEIGHT\" BORDER=\"0\">";
}


###
## TagA - リンクタグのフォーマット
#
# - SYNOPSIS
#	TagA( $href, $markUp );
#
# - ARGS
#	$href		リンク先URL
#	$markUp		マークアップ文字列
#
# - DESCRIPTION
#	リンクをリンクタグにフォーマットする．
#
# - RETURN
#	フォーマットした文字列
#
sub TagA
{
    local( $href, $markUp ) = @_;
    "<a href=\"$href\">$markUp</a>";
}


###
## TagForm - フォームタグのフォーマット
#
# - SYNOPSIS
#	TagForm( *str, *hiddenTags, $submit, $reset, *contents );
#
# - ARGS
#	*str		生成文字列の格納先
#	*tags		追加するhiddenタグを収めた連想配列
#	*submit		submitボタン文字列
#	*reset		resetボタン文字列
#	*contents	</form>の前までに挿入する文字列
#
# - DESCRIPTION
#	Formタグのフォーマット
#
sub TagForm
{
    local( *str, *tags, $submit, $reset, *contents ) = @_;

    $str = "<form action=\"$PROGRAM\" method=\"POST\">\n";
    foreach ( keys( %tags ))
    {
	$str .= "<input name=\"$_\" type=\"hidden\" value=\"$tags{$_}\">\n";
    }
    $str .= $contents;
    $str .= "<input type=\"submit\" value=\"$submit\">\n";
    $str .= "<input type=\"reset\" value=\"$reset\">\n" if $reset;
    $str .= "</form>\n";
}


###
## SendMail - メイル送信
#
# - SYNOPSIS
#	SendMail($Subject, $Message, $Id, @To);
#
# - ARGS
#	$Subject	メイルのSubject文字列
#	$Message	本文
#	$Id		引用するなら記事ID; なければ引用はなし
#	@To		宛先E-Mail addr.のリスト
#
# - DESCRIPTION
#	メイルを送信する．
#
# - RETURN
#	なし
#
sub SendMail
{
    local( $Subject, $Message, $Id, @To ) = @_;

    local( $ExtensionHeader, @ArticleBody );

    $ExtensionHeader = "X-Kb-System: $SYSTEM_NAME\n";
    if (( ! $SYS_MAILHEADBRACKET ) && $BOARDNAME && ($Id ne '' ))
    {
	$ExtensionHeader .= "X-Kb-Board: $BOARDNAME\nX-Kb-Articleid: $Id\n";
    }

    # 引用記事
    if ( $Id ne '' ) {
	$Message .= "\n--------------------\n";
	&GetArticleBody($Id, $BOARD, *ArticleBody);
	foreach(@ArticleBody)
	{
	    s/<[^>]*>//go;	# タグは要らない
	    $Message .= &HTMLDecode( $_ ) if ( $_ ne '' );
	}
    }

    local( $stat, $errstr ) = &cgi'sendMail( $MAILFROM_LABEL || $MAINT_NAME, $MAINT, $Subject, $ExtensionHeader, $Message, $MAILTO_LABEL, @To );
    &Fatal( 9, "$BOARDNAME/$Id/$errstr" ) if ( !$stat );
}


###
## KbLog - log a log
#
# - SYNOPSIS
#	&KbLog( $kinologue'severity, *msg );
#
# - ARGS
#	$kinologue'severity	severity id defined in kinologue.pl.
#	*msg			reference to msg string.
#
# - DESCRIPTION
#	log a log using kinologue.
#	exit program(not function) if failed to write log.
#
# - RETURN
#	returns nothing.
#
sub KbLog
{
    local( $severity, $msg ) = @_;

    &kinologue'KlgLog( $severity, $msg, $PROGNAME, $LOGFILE, $FF_LOG ) || &Fatal( 1000, '' ) if $SYS_LOG;
}


######################################################################
# ロジックインプリメンテーション


###
## MakeNewArticle - 新たに投稿された記事の生成
#
# - SYNOPSIS
#	MakeNewArticle($Board, $Id, $TextType, $Name, $Email, $Url, $Icon, $Subject, $Article, $Fmail);
#
# - ARGS
#	$Board		作成する記事が入る掲示板のID
#	$Id		リプライ元記事のID
#	$TextType	文書タイプ
#	$Name		投稿者名
#	$Email		投稿者E-Mail addr.
#	$Url		投稿者URL
#	$Icon		アイコンID
#	$Subject	Subject文字列
#	$Article	本文文字列
#	$Fmail		リプライがあった時にメイルで知らせるか否か('on'/'')
#
# - DESCRIPTION
#	投稿された記事を記事DBに整理する．
#
# - RETURN
#	なし
#
sub MakeNewArticle
{
    local( $Board, $Id, $TextType, $Name, $Email, $Url, $Icon, $Subject, $Article, $Fmail ) = @_;

    local( $ArticleId );

    &CheckArticle( $Board, *Name, *Email, *Url, *Subject, *Icon, *Article );

    # 新しい記事番号を取得(まだ記事番号は増えてない)
    $ArticleId = &GetNewArticleId( $Board );

    # 正規のファイルの作成
    &MakeArticleFile( $TextType, $Article, $ArticleId, $Board );

    # 新しい記事番号を書き込む
    &WriteArticleId( $ArticleId, $Board );

    # DBファイルに投稿された記事を追加
    # 通常の記事引用ならID
    &AddDBFile( $ArticleId, $Board, $Id, $^T, $Subject, $Icon, $cgi'REMOTE_HOST, $Name, $Email, $Url, $Fmail );

    $ArticleId;
}


###
## SearchArticleKeyword - 記事の検索(本文)
#
# - SYNOPSIS
#	SearchArticleKeyword($Id, $Board, @KeyList);
#
# - ARGS
#	$Id		本文を検索する記事のID
#	$Board		掲示板ID
#	@KeyList	キーワードリスト
#
# - DESCRIPTION
#	指定された記事の本文を，キーワードでAND検索する．
#
# - RETURN
#	最初にキーワードとマッチした行．マッチしなかったら空を返す．
#
sub SearchArticleKeyword
{
    local( $Id, $Board, @KeyList ) = @_;

    local( @NewKeyList, $Line, $Return, $Code, $ConvFlag, @ArticleBody );

    $ConvFlag = ( $Id !~ /^\d+$/ );

    &GetArticleBody( $Id, $Board, *ArticleBody );
    foreach ( @ArticleBody )
    {
	$Line = $_;
	if ( $ConvFlag )
	{
	    $Code = &jcode'getcode( *Line );
	    &jcode'convert( *Line, 'euc', $Code, 'z' );
	}

	# 検索
	@NewKeyList = ();
	foreach ( @KeyList )
	{
	    if ( $Line =~ /$_/i )
	    {
		# マッチした! 1行目なら覚えとく
		$Return = $Line unless $Return;
	    }
	    else
	    {
		# まだ探さなきゃ……
		push( @NewKeyList, $_ );
	    }
	}
	# 空なら抜け．
	last unless ( @KeyList = @NewKeyList );
    }

    # まだ残ってたらアウト．空なら最初のマッチした行を返す．
    @KeyList ? '' : $Return;
}


###
## CheckArticle - 入力された記事情報のチェック
#
# - SYNOPSIS
#	CheckArticle($board, *name, *eMail, *url, *subject, *icon, *article);
#
# - ARGS
#	$board		掲示板ID
#	*name		投稿者名
#	*eMail		メイルアドレス
#	*url		URL
#	*subject	Subject
#	*icon		アイコンID
#	*article	本文
#
# - DESCRIPTION
#	入力された記事をチェックする
#
# - RETURN
#	なし
#
sub CheckArticle
{
    local( $board, *name, *eMail, *url, *subject, *icon, *article ) = @_;

    local( $Tmp );

    if ( $name =~ /^\#.*$/o )
    {
        ( $Tmp, $eMail, $url ) = &GetUserInfo( $name );
	&Fatal( 6, $name ) if ( $Tmp eq '' );
	$name = $Tmp;
    }
    elsif ( $SYS_ALIAS == 2 )
    {
	# 必須のはずなのに，指定されたエイリアスが登録されていない
	&Fatal( 6, $name );
    }

    &CheckName( *name );
    &CheckEmail( *eMail );
    &CheckURL( *url );
    &CheckSubject( *subject );

    # 本文の空チェック．
    &Fatal( 2, '' ) if ( $article eq '' );

    # アイコンのチェック; おかしけりゃ「無し」に設定．
    $icon = $H_NOICON if ( !&GetIconUrlFromTitle( $icon, $board ));

    if ( $SYS_MAXARTSIZE != 0 )
    {
	local( $length ) = length( $article );
	&Fatal( 12, $length ) if ( $length > $SYS_MAXARTSIZE );
    }
}


###
## secureSubject - 安全なSubjectを作り出す
#
# - SYNOPSIS
#	secureSubject( *subject );
#
# - ARGS
#	*subject	Subject文字列
#
# - DESCRIPTION
#	$subjectを安全な文字列に変換する．
#
sub secureSubject
{
    local( *subject ) = @_;

    if ( $SYS_TAGINSUBJECT )
    {
	local( @subjectTags ) =
	    (
	     # タグ名, 閉じ必須か否か, 使用可能なfeature
	     'B',		1,	'',
	     'BR',		0,	'',
	     'CITE',		1,	'',
	     'CODE',		1,	'',
	     'EM',		1,	'',
	     'FONT',		1,	'SIZE/COLOR',
	     'H2',		1,	'ALIGN',
	     'H3',		1,	'ALIGN',
	     'H4',		1,	'ALIGN',
	     'H5',		1,	'ALIGN',
	     'H6',		1,	'ALIGN',
	     'I',		1,	'',
	     'IMG',		0,	'SRC/ALT/ALIGN/WIDTH/HEIGHT/BORDER',
	     'STRONG',		1,	'',
	     'TT',		1,	'',
	     'VAR',		1,	'',
	     );
	
	local( %sNeedVec, %sFeatureVec, $tag );
	while( @subjectTags )
	{
	    $tag = shift( @subjectTags );
	    $sNeedVec{ $tag } = shift( @subjectTags );
	    $sFeatureVec{ $tag } = shift( @subjectTags );
	}

	# secrurity check
	&cgi'SecureHtmlEx( *subject, *sNeedVec, *sFeatureVec );
    }
    else
    {
	$subject = &HTMLEncode( $subject );	# no tags are allowed.
    }
}


###
## secureArticle - 安全なArticleを作り出す
#
# - SYNOPSIS
#	secureArticle( *article, $textType );
#
# - ARGS
#	*article	Article文字列
#	$textType	入力形式
#
# - DESCRIPTION
#	$articleを安全な文字列に変換する．
#
sub secureArticle
{
    local( *article, $textType ) = @_;

    local( @articleTags ) =
	(
	 # タグ名, 閉じ必須か否か, 使用可能なfeature
	 'A',		1,	'HREF/NAME/TARGET',
	 'ADDRESS',		1,	'',
	 'B',		1,	'',
	 'BLOCKQUOTE',	1,	'',
	 'BR',		0,	'',
	 'CITE',		1,	'',
	 'CODE',		1,	'',
	 'DD',		0,	'',
	 'DIR',		1,	'',
	 'DL',		1,	'COMPACT',
	 'DT',		0,	'',
	 'EM',		1,	'',
	 'FONT',		1,	'SIZE/COLOR',
	 'H1',		1,	'ALIGN',
	 'H2',		1,	'ALIGN',
	 'H3',		1,	'ALIGN',
	 'H4',		1,	'ALIGN',
	 'H5',		1,	'ALIGN',
	 'H6',		1,	'ALIGN',
	 'HR',		0,	'SIZE/WIDTH/ALIGN',
	 'I',		1,	'',
	 'IMG',		0,	'SRC/ALT/ALIGN/WIDTH/HEIGHT/BORDER',
	 'KBD',		1,	'',
	 'LI',		0,	'TYPE/VALUE',
	 'LISTING',		1,	'',
	 'MENU',		1,	'',
	 'OL',		1,	'START',
	 'P',		0,	'ALIGN',
	 'PRE',		1,	'',
	 'SAMP',		1,	'',
	 'STRONG',		1,	'',
	 'TT',		1,	'',
	 'UL',		1,	'',
	 'VAR',		1,	'',
	 'XMP',		1,	'',
	 );
	
    local( %aNeedVec, %aFeatureVec, $tag );
    while( @articleTags )
    {
	$tag = shift( @articleTags );
	$aNeedVec{ $tag } = shift( @articleTags );
	$aFeatureVec{ $tag } = shift( @articleTags );
    }
    
    if (( !$SYS_TEXTTYPE ) || ( $textType eq $H_TTLABEL[0] ))
    {
	# pre-formatted
	&PlainArticleToPreFormatted( *article );
	# <URL:>の処理
	$article = &ArticleEncode( $article );
	$article =~ s/<URL:([^>][^>]*)>/$1/gi;
    }
    elsif ( $textType eq $H_TTLABEL[1] )
    {
	# convert to html
	&PlainArticleToHtml( *article );
	# <URL:>の処理
	$article = &ArticleEncode( $article );
	# secrurity check
	&cgi'SecureHtmlEx( *article, *aNeedVec, *aFeatureVec );
    }
    elsif ( $textType eq $H_TTLABEL[2] )
    {
	# <URL:>の処理
	$article = &ArticleEncode( $article );
	# secrurity check
	&cgi'SecureHtmlEx( *article, *aNeedVec, *aFeatureVec );
    }
    else
    {
	&Fatal( 0, 'must not be reached...' );
    }
}


###
## AliasCheck - ユーザエイリアスのチェック
#
# - SYNOPSIS
#	AliasCheck($A, $N, $E, $U);
#
# - ARGS
#	$A	エイリアス名
#	$N	名前
#	$E	E-Mail addr.
#	$U	URL
#
# - DESCRIPTION
#	ユーザエイリアスのデータ(エイリアス名，名前，E-Mail，URL)
#	をチェックする．
#
# - RETURN
#	なし
#
sub AliasCheck
{
    local( $A, $N, $E, $U ) = @_;
    &CheckAlias( *A );
    &CheckName( *N );
    &CheckEmail( *E );
    &CheckURL( *U );
}


###
## CheckAlias - 文字列チェック: エイリアス
#
# - SYNOPSIS
#	CheckAlias(*String);
#
# - ARGS
#	*String		エイリアス文字列
#
# - DESCRIPTION
#	エイリアスの文字列チェックを行なう．
#	不正な文字列だったらエラー表示ルーチンへ．
#	(アプリケーション/UIを分離したほうがいいかな?)
#
# - RETURN
#	なし
#
sub CheckAlias
{
    local( *String ) = @_;

    &Fatal( 2, '' ) if ( !$String );
    &Fatal( 7, $H_ALIAS ) if ( $String !~ ( /^\#/ ));

    # 1文字じゃだめ
    &Fatal( 7, $H_ALIAS ) if ( length( $String ) < 2 );

    &Fatal( 3, '' ) if ( $String =~ m/[\t\n]/o );
}


###
## CheckSubject - 文字列チェック: Subject
#
# - SYNOPSIS
#	CheckSubject(*String);
#
# - ARGS
#	*String		Subject文字列
#
# - DESCRIPTION
#	Subjectの文字列チェックを行なう．
#	不正な文字列だったらエラー表示ルーチンへ．
#	(アプリケーション/UIを分離したほうがいいかな?)
#
# - RETURN
#	なし
#
sub CheckSubject
{
    local( *String ) = @_;

    &Fatal( 2, '' ) unless $String;
    &Fatal( 3, '' ) if ( $String =~ m/[\t\n]/o );

    if ( !$SYS_TAGINSUBJECT )
    {
	&Fatal( 4, '' ) if ( $String =~ m/[<>]/o );
    }
}


###
## CheckName - 文字列チェック: 投稿者名
#
# - SYNOPSIS
#	CheckName(*String);
#
# - ARGS
#	*String		投稿者名文字列
#
# - DESCRIPTION
#	投稿者名の文字列チェックを行なう．
#	不正な文字列だったらエラー表示ルーチンへ．
#	(アプリケーション/UIを分離したほうがいいかな?)
#
# - RETURN
#	なし
#
sub CheckName
{
    local( *String ) = @_;

    &Fatal( 2, '' ) if ( !$String );
    &Fatal( 3, '' ) if ( $String =~ m/[\t\n]/o );
}


###
## CheckEmail - 文字列チェック: E-Mail addr.
#
# - SYNOPSIS
#	CheckEmail(*String);
#
# - ARGS
#	*String		E-Mail addr.文字列
#
# - DESCRIPTION
#	E-Mail addr.の文字列チェックを行なう．
#	不正な文字列だったらエラー表示ルーチンへ．
#	(アプリケーション/UIを分離したほうがいいかな?)
#
# - RETURN
#	なし
#
sub CheckEmail
{
    local( *String ) = @_;

    if ( $SYS_POSTERMAIL ) {
	&Fatal( 2, '' ) if ( !$String );
	# `@'が入ってなきゃアウト
	&Fatal( 7, 'E-Mail' ) if ( $String !~ /@/ );
    }
    &Fatal( 3, '' ) if ( $String =~ m/[\t\n]/o );
}


###
## CheckURL - 文字列チェック: URL
#
# - SYNOPSIS
#	CheckURL(*String);
#
# - ARGS
#	*String		URL文字列
#
# - DESCRIPTION
#	URLの文字列チェックを行なう．ただし空チェックだけ．
#	中身のチェックにはIsUrlを呼び出す．
#	不正な文字列だったらエラー表示ルーチンへ．
#	(アプリケーション/UIを分離したほうがいいかな?)
#
# - RETURN
#	なし
#
sub CheckURL
{
    local( *String ) = @_;

    # http://だけの場合は空にしてしまう．
    $String = '' if ( $String =~ m!^http://$!oi );
    &Fatal( 7, 'URL' ) if (( $String ne '' ) && ( !&IsUrl( $String )));
}


###
## IsUrl - URLの構造をチェック
#
# - SYNOPSIS
#	IsUrl($String);
#
# - ARGS
#	$String		URL文字列
#
# - DESCRIPTION
#	URLの文字列チェックを行なう．
#	予め指定された(@URL_SCHEME)Schemeのものであるか否かをチェック．
#	telnet接続等は，しない．
#
# - RETURN
#	正しいURLであれば1，そうでなければ0
#
sub IsUrl
{
    local( $String ) = @_;

    local( $IsUrl ) = 0;
    local( $Scheme );
    foreach $Scheme ( @URL_SCHEME )
    {
	$IsUrl = 1 if ( $String =~ m!^$Scheme://!i );
    }
    $IsUrl;
}


###
## GetFollowIdTree - リプライ記事の木構造を取得
#
# - SYNOPSIS
#	GetFollowIdTree($Id, *Time);
#
# - ARGS
#	$Id	記事ID
#	*Time	最終投稿時刻へのリファレンス
#
# - DESCRIPTION
#	指定された記事のリプライ記事を，そのIDの木構造フォーマットで取り出す．
#	*Timeには，木構造中の，最新の記事の投稿時間が設定される．
#
#	例: * (←指定された記事)
#	    +--a
#	    |  +--b
#	    |  |  +--c
#	    |  |  +--d
#	    |  |
#	    |  +--e
#	    |     +--f
#	    +--g
#	       +--h
#
#	→ ( a ( b ( c d ) e ( f ) ) g ( h ) )
#
# - RETURN
#	木構造を表すリスト
#
sub GetFollowIdTree
{
    local( $Id, *Time ) = @_;
    # 再帰的に木構造を取り出す．
    return( '(', &GetFollowIdTreeMain( $Id, *Time ), ')' );
}

sub GetFollowIdTreeMain
{
    local( $Id, *Time ) = @_;
    local( @AidList, @Result, @ChildResult, $lastFollowMsgFid, $lastFollowMsgAids, $lastFollowMsgDate );

    # 再帰停止条件
    return if ( $Id eq '' );

    # フォロー記事取り出し
    @AidList = split( /,/, $DB_AIDS{$Id} );

    # なけりゃ停止
    return $Id if ( !@AidList );

    # 再帰
    @Result = ( $Id, '(' );
    @ChildResult = ();
    foreach ( @AidList )
    {
	@ChildResult = &GetFollowIdTreeMain( $_, *Time );
	push( @Result, @ChildResult ) if @ChildResult;
    }

    # 最後のフォロー記事のタイムスタンプを見る
    ( $lastFollowMsgFid, $lastFollowMsgAids, $lastFollowMsgDate ) = &GetArticlesInfo( $AidList[ $#AidList ] );
    $Time = $lastFollowMsgDate if ( $Time < $lastFollowMsgDate );

    return( @Result, ')' );
}


###
## GetReplySubject - リプライSubjectの生成
#
# - SYNOPSIS
#	GetReplySubject($Id);
#
# - ARGS
#	$Id	記事ID
#
# - DESCRIPTION
#	あるIdの記事からSubjectを取ってきて，先頭に「Re:」を1つだけつけて返す．
#
# - RETURN
#	生成したSubject文字列
#
sub GetReplySubject
{
    local( $Id ) = @_;

    local( $dFid, $dAids, $dDate, $dSubject ) = &GetArticlesInfo( $Id );

    # Re:を取り除き，
    $dSubject =~ s/^Re:\s*//oi;

    # TAG用エンコードして，
    &TAGEncode( *dSubject );

    # 先頭に「Re: 」をくっつけて返す．
    "Re: $dSubject";
}


###
## GetModifiedTime - ある記事の最終更新時刻(UTC)を取得
#
# - SYNOPSIS
#	GetModifiedTime($Id, $Board);
#
# - ARGS
#	$Id		記事ID
#	$Board		掲示板ID
#
# - DESCRIPTION
#	あるIDの記事から，最終更新UTCを取ってくる．
#
# - RETURN
#	その記事ファイルの最終更新時刻(UTC)
#
sub GetModifiedTime
{
    local( $Id, $Board ) = @_;

    # 86400 = 24 * 60 * 60
    $^T - ( -M &GetArticleFileName( $Id, $Board )) * 86400;
}


###
## GetDateTimeFormatFromUtc - UTCから時間を表す文字列を取得
#
# - SYNOPSIS
#	GetDateTimeFormatFromUtc($Utc);
#
# - ARGS
#	$Utc		時刻(UTC)
#
# - DESCRIPTION
#	UTCを時分秒に分割し，フォーマットして文字列として返す．
#	古いバージョンのKINOBOARDSでは，
#	DB中に時刻を表す文字列(not UTC)がそのまま入っているが，
#	それが渡された場合(UTCでなかった場合)は，そのまま返す．
#
# - RETURN
#	時刻を表わす文字列
#
sub GetDateTimeFormatFromUtc
{
    local( $Utc ) = @_;

    local( $Sec, $Min, $Hour, $Mday, $Mon, $Year, $Wday, $Yday, $Isdst );

    # 古い時代のものらしい．
    return $Utc if ( $Utc !~ m/^\d+$/ );

    # 変換
    ( $Sec, $Min, $Hour, $Mday, $Mon, $Year, $Wday, $Yday, $Isdst ) = localtime( $Utc );

    sprintf( "%d/%d/%d(%02d:%02d)", $Year, $Mon + 1, $Mday, $Hour, $Min );

    # for MM/DD
    # sprintf( "%d/%d(%02d:%02d)", $Mon + 1, $Mday, $Hour, $Min );

    # for Y2k!
    # sprintf( "%d/%d/%d(%02d:%02d)", $Year+1900, $Mon + 1, $Mday, $Hour, $Min );
}


###
## GetUtcFromOldDateTimeFormat - 時刻のUTCへの正規化
#
# - SYNOPSIS
#	GetUtcFromOldDateTimeFormat($Time);
#
# - ARGS
#	$Time		時間を表わす文字列
#
# - DESCRIPTION
#	古いバージョンのKINOBOARDSでは，
#	DB中に時刻を表す文字列がそのまま(UTCでなく)入っている．
#	これが渡された場合は適当なUTC(854477921 = 97/01/29 03:58)を返す．
#	UTCが渡されたらそのままそのUTCを返す．
#
# - RETURN
#	時刻(UTC)
#
sub GetUtcFromOldDateTimeFormat
{
    local( $Time ) = @_;

    # 新規らしい
    return $Time if ( $Time =~ m/^\d+$/ );

    # 適当
    854477921;
}


###
## DQEncode/DQDecode - 特殊文字のEncodeとDecode
#
# - SYNOPSIS
#	DQEncode($Str);
#	DQDecode($Str);
#
# - ARGS
#	$Str	Encode/Decodeする文字列
#
# - DESCRIPTION
#	HTMLの特殊文字(", >, <, &)をEncode/Decodeする．
#
# - RETURN
#	Encode/Decodeした文字列
#
sub DQEncode
{
    local( $Str ) = @_;
    $Str =~ s/\"/__dq__/go;
    $Str =~ s/\>/__gt__/go;
    $Str =~ s/\</__lt__/go;
    $Str =~ s/\&/__amp__/go;
    $Str;
}

sub DQDecode
{
    local( $Str ) = @_;
    $Str =~ s/__dq__/\"/go;
    $Str =~ s/__gt__/\>/go;
    $Str =~ s/__lt__/\</go;
    $Str =~ s/__amp__/\&/go;
    $Str;
}


###
## HTMLEncode/HTMLDecode - 特殊文字のHTML用EncodeとDecode
#
# - SYNOPSIS
#	HTMLEncode($Str);
#	HTMLDecode($Str);
#
# - ARGS
#	$Str	HTML用Encode/Decodeする文字列
#
# - DESCRIPTION
#	HTMLの特殊文字(", >, <, &)をHTML用にEncode/Decodeする．
#
# - RETURN
#	Encode/Decodeした文字列
#
sub HTMLEncode
{
    local( $_ ) = @_;
    s/\&/&amp;/go;
    s/\"/&quot;/go;
    s/\>/&gt;/go;
    s/\</&lt;/go;
    $_;
}

sub HTMLDecode
{
    local( $_ ) = @_;
    s/&quot;/\"/gio;
    s/&gt;/\>/gio;
    s/&lt;/\</gio;
    s/&amp;/\&/gio;
    $_;
}


###
## TAGEncode - 特殊文字のTAG埋め込み用Encode
#
# - SYNOPSIS
#	TAGEncode( *str );
#
# - ARGS
#	*str	TAG埋め込み用Encodeする文字列
#
# - DESCRIPTION
#	TAG埋め込み（<input value="ここの文字列">）用に，"と&を取り除く．
#	Encodeと言いながら，今のところDecodeすることはできず，
#	ただ削除するのみ
#
# - RETURN
#	Encodeした文字列
#
sub TAGEncode
{
    local( *str ) = @_;
    $str =~ s/[\&\"]//go;		# 引用のための変換
    $str =~ s/<[^>]*>//go;		# 引用のための変換
}


###
###
## ArticleEncode - 記事のEncode
#
# - SYNOPSIS
#	ArticleEncode($Article);
#
# - ARGS
#	$Article	Encodeする記事本文
#
# - DESCRIPTION
#	記事中のURL(<URL:〜>)を，リンクに変換する．
#
# - RETURN
#	Encodeされた文字列
#
sub ArticleEncode
{
    local( $article ) = @_;

    local( $ret ) = $article;
    local( $url, $urlMatch, $str, @cache );
    while ( $article =~ m/<URL:([^>][^>]*)>/gi )
    {
	$url = $1;
	( $urlMatch = $url ) =~ s/([?+*^\\\[\]\|()])/\\$1/go;
	next if ( grep( /^$urlMatch$/, @cache ));
	push( @cache, $url );
	$str = "<URL:$url>";
	$str = &TagA( $url, $str ) if ( &IsUrl( $urlMatch ));
	$ret =~ s/<URL:$urlMatch>/$str/gi;
    }

    $ret;
}


###
## PlainArticleToPreFormatted - Plain記事をpre formatted textに変換
#
# - SYNOPSIS
#	PlainArticleToPreFormatted(*Article);
#
# - ARGS
#	*Article	変換する記事本文
#
# - DESCRIPTION
#	記事先頭と末尾の無意味な改行を取り除く．
#	タグはHTML encodeする．
#	全体を<pre>と</pre>で囲む．
#	*Articleを破壊する．
#
# - RETURN
#	なし
#
sub PlainArticleToPreFormatted
{
    local( *Article ) = @_;

    $Article =~ s/\n*$//o;
    $Article =~ s/<URL:([^>][^>]*)>/__URL__$COLSEP$1$COLSEP/gi;
    $Article = &HTMLEncode( $Article );	# no tags are allowed.
    $Article =~ s/__URL__$COLSEP([^$COLSEP][^$COLSEP]*)$COLSEP/<URL:$1>/gi;
    $Article = "<pre>\n" . $Article . "</pre>";
}


###
## PlainArticleToHtml - Plain記事をHTMLに変換
#
# - SYNOPSIS
#	PlainArticleToHtml(*Article);
#
# - ARGS
#	*Article	変換する記事本文
#
# - DESCRIPTION
#	記事末尾の無意味な改行を取り除く．
#	各段落を<p>で囲む．
#	*Articleを破壊する．
#
# - RETURN
#	なし
#
sub PlainArticleToHtml
{
    local( *Article ) = @_;
    $Article =~ s/^\n*//o;
    $Article =~ s/\n*$//o;
    $Article =~ s/\n/<br>\n/go;
    $Article =~ s/<br>\n<br>\n(<br>\n)*/<\/p>\n\n<p>/go;
    $Article = "<p>$Article</p>";
}


###
## DeleteArticle - 記事の削除
#
# - SYNOPSIS
#	DeleteArticle($Id, $ThreadFlag);
#
# - ARGS
#	$Id		削除記事ID
#	$Board		掲示板ID
#	$ThreadFlag	リプライも消すか否か
#
# - DESCRIPTION
#	削除すべき記事IDを収集した後，DBを更新する．
#
# - RETURN
#	なし
#
sub DeleteArticle
{
    local( $Id, $Board, $ThreadFlag ) = @_;

    local( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $dId, @Target, $TargetId );

    # 記事情報の取得
    ( $Fid, $Aids, $Date, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url ) = &GetArticlesInfo( $Id );

    # データの書き換え(必要なら娘も)
    @Target = ( $Id );
    foreach $TargetId ( @Target )
    {
	foreach ( 0 .. $#DB_ID )
	{
	    # IDを取り出す
	    $dId = $DB_ID[$_];
	    # フォロー記事リストの中から，削除する記事のIDを取り除く
	    $DB_AIDS{$dId} = join( ',', grep(( !/^$TargetId$/o ), split( /,/, $DB_AIDS{$dId} )));
	    # 元記事から削除記事のIDを取り除く
	    $DB_FID{$dId} = '' if ( $DB_FID{$dId} eq $TargetId );
	    $DB_FID{$dId} =~ s/,$TargetId,.*$//;
	    $DB_FID{$dId} =~ s/^$TargetId,.*$//;
	    $DB_FID{$dId} =~ s/,$TargetId$//;
	    # 娘も対象とする
	    push( @Target, split( /,/, $DB_AIDS{$dId} )) if ( $ThreadFlag && ( $dId eq $TargetId ));
	}
    }

    # DBを更新する．
    &DeleteArticleFromDbFile( $Board, *Target );
}


###
## SupersedeArticle - 記事を訂正する
#
# - SYNOPSIS
#	SupersedeArticle;
#
# - ARGS
#	なし
#
# - DESCRIPTION
#	記事を訂正する．
#
# - RETURN
#	なし
#
sub SupersedeArticle
{
    local( $Board, $Id, $TextType, $Name, $Email, $Url, $Icon, $Subject, $Article, $Fmail ) = @_;

    local( $SupersedeId, $File, $SupersedeFile );

    # 入力された記事情報のチェック
    &CheckArticle( $Board, *Name, *Email, *Url, *Subject, *Icon, *Article );

    # DBファイルを訂正
    $SupersedeId = &SupersedeDbFile( $Board, $Id, $^T, $Subject, $Icon, $cgi'REMOTE_HOST, $Name, $Email, $Url, $Fmail );

    # ex. 「100」→「100_5」
    $File = &GetArticleFileName( $Id, $Board );
    $SupersedeFile = &GetArticleFileName( sprintf( "%s_%s", $Id, $SupersedeId ), $Board );
    rename( $File, $SupersedeFile ) || &Fatal( 14, "$File -&gt; $SupersedeFile" );

    # 正規のファイルの作成
    &MakeArticleFile( $TextType, $Article, $Id, $Board );

    $Id;
}


###
## ReLinkExec - 記事のかけかえ実施
#
# - SYNOPSIS
#	ReLinkExec($FromId, $ToId, $Board);
#
# - ARGS
#	$FromId		かけかえ元記事ID
#	$ToId		かけかえ先記事ID
#	$Board		掲示板ID
#
# - DESCRIPTION
#	記事をリプライ-元記事関係をかけかえる．
#
# - RETURN
#	なし
#
sub ReLinkExec
{
    local( $FromId, $ToId, $Board ) = @_;

    local( $dId, @Daughters, $DaughterId );

    # 循環記事の禁止
    &FatalPriv( 50, '' ) if ( grep( /^$FromId$/, split( /,/, $DB_FID{$ToId} )));

    # データ書き換え
    foreach ( 0 .. $#DB_ID )
    {
	# IDを取り出す
	$dId = $DB_ID[$_];
	# フォロー記事リストの中から，移動する記事のIDを取り除く
	$DB_AIDS{$dId} = join( ',', grep(( !/^$FromId$/o ), split( /,/, $DB_AIDS{$dId} )));
    }

    # 必要なら娘をとりだしておく
    @Daughters = split( /,/, $DB_AIDS{$FromId} ) if $DB_FID{$FromId};

    # 該当記事のリプライ先を変更する
    if ( $ToId eq '' )
    {
	$DB_FID{$FromId} = '';
    }
    elsif ( $DB_FID{$ToId} eq '' )
    {
	$DB_FID{$FromId} = "$ToId";
    }
    else
    {
	$DB_FID{$FromId} = "$ToId,$DB_FID{$ToId}";
    }

    # 該当記事の娘についても，リプライ先を変更する
    while ( $DaughterId = shift( @Daughters ))
    {
	# 孫娘も……
	push( @Daughters, split( /,/, $DB_AIDS{$DaughterId} ));
	# 書き換え
	if (( $DB_FID{$DaughterId} eq $FromId )
	    || ( $DB_FID{$DaughterId} =~ /^$FromId,/ ))
	{
	    $DB_FID{$DaughterId} = $DB_FID{$FromId} ? "$FromId,$DB_FID{$FromId}" : "$FromId";
	}
	elsif (( $DB_FID{$DaughterId} =~ /^(.*),$FromId$/ )
	       || ( $DB_FID{$DaughterId} =~ /^(.*),$FromId,/ ))
	{
	    $DB_FID{$DaughterId} = $DB_FID{$FromId} ? "$1,$FromId,$DB_FID{$FromId}" : "$1,$FromId";
	}
    }

    # リプライ先になった記事のフォロー記事群に追加する
    $DB_AIDS{$ToId} = ( $DB_AIDS{$ToId} ne '' ) ? "$DB_AIDS{$ToId},$FromId" : "$FromId";

    # 記事DBを更新する
    &UpdateArticleDb( $Board );

    # DB書き換えたので，キャッシュし直す
    &DbCache( $Board );
}


###
## ReOrderExec - 記事の移動実施
#
# - SYNOPSIS
#	ReOrderExec($FromId, $ToId, $Board);
#
# - ARGS
#	$FromId		移動元記事ID
#	$ToId		移動先記事ID
#	$Board		掲示板ID
#
# - DESCRIPTION
#	指定された記事を，指定された記事の次に移動する．
#
# - RETURN
#	なし
#
sub ReOrderExec
{
    local( $FromId, $ToId, $Board ) = @_;

    local( @Move );

    # 移動する記事たちを集める
    @Move = ( $FromId, &CollectDaughters( $FromId ));

    # 移動させる
    &ReOrderArticleDb( $Board, $ToId, *Move );

    # DB書き換えたので，キャッシュし直す
    &DbCache( $Board );
}


###
## CollectDaughters - 娘ノードのリストを集める
#
# - SYNOPSIS
#	CollectDaughters($Id);
#
# - ARGS
#	$Id		記事ID
#
# - DESCRIPTION
#	娘ノードのリストを集める
#
# - RETURN
#	娘ノード集合のリスト
#
sub CollectDaughters
{
    local( $Id ) = @_;
    local( @Return );
    foreach ( split(/,/, $DB_AIDS{$Id} ))
    {
	push( @Return, $_ );
	push( @Return, &CollectDaughters( $_ )) if ( $DB_AIDS{$_} ne '' );
    }
    @Return;
}


###
## GetNewArticleId - 新着記事IDの決定
#
# - SYNOPSIS
#	GetNewArticleId($Board);
#
# - ARGS
#	$Board		掲示板ID
#
# - DESCRIPTION
#	従来の最新記事IDを1増やした，新しい記事番号を返す．
#
# - RETURN
#	新着記事のID
#
sub GetNewArticleId
{
    local( $Board ) = @_;
    &GetArticleId( $Board ) + 1;
}


######################################################################
# データインプリメンテーション


###
## DbCache - 記事DBの全読み込み
#
# - SYNOPSIS
#	DbCache($Board);
#
# - ARGS
#	$Board		掲示板ID
#
# - DESCRIPTION
#	主に起動時に呼び出され，記事DBの内容を大域変数にキャッシュする．
#
# - RETURN
#	なし
#
$BOARD_DB_CACHE = 0;

sub DbCache
{
    return if $BOARD_DB_CACHE;

    local( $Board ) = @_;

    local( $DBFile, $dId, $dFid, $dAids, $dDate, $dTitle, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail );

    @DB_ID = %DB_FID = %DB_AIDS = %DB_DATE = %DB_TITLE = %DB_ICON = %DB_REMOTEHOST = %DB_NAME = %DB_EMAIL = %DB_URL = %DB_FMAIL = %DB_NEW = ();

    local( $i ) = 0;
    $DBFile = &GetPath( $Board, $DB_FILE_NAME );
    open( DB, "<$DBFile" ) || &Fatal( 1, $DBFile );
    while ( <DB> )
    {
	next if (/^\#/o || /^$/o);
	chop;
	( $dId, $dFid, $dAids, $dDate, $dTitle, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail ) = split( /\t/, $_, 11 );
	next if ( $dId eq '' );

	$DB_ID[$i++] = $dId;
	$DB_FID{$dId} = $dFid;
	$DB_AIDS{$dId} = $dAids;
	$DB_DATE{$dId} = $dDate || &GetModifiedTime($dId, $Board);
	$DB_TITLE{$dId} = $dTitle || $dId;
	$DB_ICON{$dId} = $dIcon;
	$DB_REMOTEHOST{$dId} = $dRemoteHost;
	$DB_NAME{$dId} = $dName || $MAINT_NAME;
	$DB_EMAIL{$dId} = $dEmail;
	$DB_URL{$dId} = $dUrl;
	$DB_FMAIL{$dId} = $dFmail;
    }
    close DB;

    if ( $SYS_NEWICON )
    {
	local( $from ) = ( $#DB_ID >= $SYS_NEWICON )?
	    $#DB_ID - $SYS_NEWICON + 1 : 0;
	foreach ( $from .. $#DB_ID ) { $DB_NEW{ $DB_ID[$_] } = 1; }
    }

    $BOARD_DB_CACHE = 1;		# cached
}


###
## GetArticlesInfo - 記事DBの読み込み
#
# - SYNOPSIS
#	GetArticlesInfo($Id);
#
# - ARGS
#	$Id	記事ID
#
# - DESCRIPTION
#	記事DBを読み込んで，情報を取り出す．
#	実際はキャッシュから読み出すだけ．
#
# - RETURN
#	記事情報のリスト
#		リプライ元記事ID
#		この記事にリプライした記事のIDのリスト(「,」区切り)
#		投稿時間(UTC)
#		Subject
#		アイコンID
#		投稿ホスト
#		投稿者名
#		投稿者E-Mail
#		投稿者URL
#		リプライがあった時に投稿者にメイルを送るか否か
#
sub GetArticlesInfo
{
    local( $Id ) = @_;
    ( $DB_FID{$Id}, $DB_AIDS{$Id}, $DB_DATE{$Id}, $DB_TITLE{$Id}, $DB_ICON{$Id}, $DB_REMOTEHOST{$Id}, $DB_NAME{$Id}, $DB_EMAIL{$Id}, $DB_URL{$Id}, $DB_FMAIL{$Id} );
}


###
## AddDBFile - 記事DBへの追加
#
# - SYNOPSIS
#	AddDBFile($Id, $Board, $Fid, $InputDate, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $Fmail);
#
# - ARGS
#	$Id		記事ID
#	$Board		掲示板ID
#	$Fid		リプライ元の記事ID
#	$InputDate	書き込み日付(UTC)
#	$Subject	Subject
#	$Icon		アイコンID
#	$RemoteHost	書き込みホスト(IP addr.かFQDNかはhttpサーバ次第)
#	$Name		投稿者名
#	$Email		投稿者E-Mail addr.
#	$Url		投稿者URL
#	$Fmail		リプライ時にメイルを送るか否か(''/'on')
#
# - DESCRIPTION
#	記事DBに記事を追加する．
#
# - RETURN
#	なし
#
sub AddDBFile
{
    local( $Id, $Board, $Fid, $InputDate, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $Fmail ) = @_;

    local( $dId, $dFid, $dAids, $dInputDate, $dSubject, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail, $mdName, $mdEmail, $mdInputDate, $mdSubject, $mdIcon, $mdId, $FidList, $FFid, $File, $TmpFile, @FollowMailTo, @FFid, @ArriveMail );

    # リプライ元のリプライ元，を取ってくる
    if ( $Fid ne '' )
    {
	( $FFid ) = &GetArticlesInfo( $Fid );
	@FFid = split( /,/, $FFid );
    }

    $FidList = $Fid;

    $File = &GetPath( $Board, $DB_FILE_NAME );
    $TmpFile = &GetPath( $Board, "$DB_FILE_NAME.$TMPFILE_SUFFIX$$" );
    open( DBTMP, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    open( DB, "<$File" ) || &Fatal( 1, $File );
    while ( <DB> )
    {
	print( DBTMP "$_" ), next if ( /^\#/o || /^$/o );
	chop;

	( $dId, $dFid, $dAids, $dInputDate, $dSubject, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail ) = split( /\t/, $_ );
	
	# フォロー先記事が見つかったら，
	if (( $dId ne '' ) && ( $dId eq $Fid ))
	{
	    # その記事のフォロー記事IDリストに加える(カンマ区切り)
	    if ( $dAids ne '' )
	    {
		$dAids .= ",$Id";
	    }
	    else
	    {
		$dAids = $Id;
	    }

	    # 元記事のフォロー先リストを取ってきて元記事を加え，
	    # 新記事のフォロー先リストを作る
	    if ( $dFid ne '' )
	    {
		$FidList = "$dId,$dFid";
	    }

	    if ( $SYS_MAIL & 2 )
	    {
		# メイル送信のためにキャッシュ
		$mdName = $dName;
		$mdEmail = $dEmail;
		$mdInputDate = $dInputDate;
		$mdSubject = $dSubject;
		$mdIcon = $dIcon;
		$mdId = $dId;
		push( @FollowMailTo, $dEmail ) if ( $dFmail ne '' );
	    }
	}

	# DBに書き加える
	printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $dId, $dFid, $dAids, $dInputDate, $dSubject, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail );

	# リプライ元のリプライ元，かつメイル送信の必要があれば，宛先を保存
	if (( $SYS_MAIL & 2 ) && @FFid && $dFmail && $dEmail && ( grep( /^$dId$/, @FFid )) && ( !grep( /^$dEmail$/, @FollowMailTo ))) {
	    push( @FollowMailTo, $dEmail );
	}
    }

    # 新しい記事のデータを書き加える．
    printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $Id, $FidList, '', $InputDate, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $Fmail );

    # close Files.
    close DB;
    close DBTMP;

    # DBを更新する
    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );

    # 必要なら投稿があったことをメイルする
    if ( $SYS_MAIL & 1 )
    {
	&GetArriveMailTo( 0, $Board, *ArriveMail );
	&ArriveMail( $Name, $Email, $Subject, $Icon, $Id, @ArriveMail ) if @ArriveMail;
    }

    # 必要なら反応があったことをメイルする
    if (( $SYS_MAIL & 2 ) && @FollowMailTo )
    {
	&FollowMail( $mdName, $mdEmail, $mdInputDate, $mdSubject, $mdIcon, $mdId, $Name, $Email, $Subject, $Icon, $Id, @FollowMailTo );
    }
}


###
## UpdateArticleDb - 記事DBの全更新
#
# - SYNOPSIS
#	UpdateArticleDb($Board);
#
# - ARGS
#	$Board		掲示板ID
#
# - DESCRIPTION
#	記事DBを，新たな記事データで全更新する．
#	書き込む記事データはキャッシュされているもの．
#
# - RETURN
#	なし
#
sub UpdateArticleDb
{
    local( $Board ) = @_;

    local( $File, $TmpFile, $dId );

    $File = &GetPath($Board, $DB_FILE_NAME);
    $TmpFile = &GetPath( $Board, "$DB_FILE_NAME.$TMPFILE_SUFFIX$$" );
    open( DBTMP, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    open( DB, "<$File" ) || &Fatal( 1, $File );
    while ( <DB> )
    {
	print( DBTMP "$_" ), next if ( /^\#/o || /^$/o );

	# Idを取り出す
	chop;
	( $dId = $_ ) =~ s/\t.*$//;

	# DBに書き加える
	printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $dId, $DB_FID{$dId}, $DB_AIDS{$dId}, $DB_DATE{$dId}, $DB_TITLE{$dId}, $DB_ICON{$dId}, $DB_REMOTEHOST{$dId}, $DB_NAME{$dId}, $DB_EMAIL{$dId}, $DB_URL{$dId}, $DB_FMAIL{$dId} );
    }

    # close Files.
    close DB;
    close DBTMP;

    # DBを更新する
    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );
}


###
## DeleteArticleFromDbFile - 記事DBの更新
#
# - SYNOPSIS
#	DeleteArticleFromDbFile($Board, *Target);
#
# - ARGS
#	$Board		掲示板ID
#	*Target		削除する記事IDのリスト
#
# - DESCRIPTION
#	記事DBから指定された記事群のエントリを削除する．
#
# - RETURN
#	なし
#
sub DeleteArticleFromDbFile
{
    local( $Board, *Target ) = @_;

    local( $File, $TmpFile, $dId );

    $File = &GetPath( $Board, $DB_FILE_NAME );
    $TmpFile = &GetPath( $Board, "$DB_FILE_NAME.$TMPFILE_SUFFIX$$" );
    open( DBTMP, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    open( DB, "<$File" ) || &Fatal( 1, $File );
    while ( <DB> )
    {
	print( DBTMP "$_" ), next if ( /^\#/o || /^$/o );

	# Idを取り出す
	chop;
	( $dId = $_ ) =~ s/\t.*$//;

	# 該当記事はコメントアウト
	print( DBTMP "#" ) if ( grep( /^$dId$/, @Target ));
	printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $dId, $DB_FID{$dId}, $DB_AIDS{$dId}, $DB_DATE{$dId}, $DB_TITLE{$dId}, $DB_ICON{$dId}, $DB_REMOTEHOST{$dId}, $DB_NAME{$dId}, $DB_EMAIL{$dId}, $DB_URL{$dId}, $DB_FMAIL{$dId} );

    }

    # close Files.
    close DB;
    close DBTMP;

    # DBを更新する
    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );
}


###
## ReOrderArticleDb - 記事DBの順序変更
#
# - SYNOPSIS
#	ReOrderArticleDb($Board, $Id, *Move);
#
# - ARGS
#	$Board		掲示板ID
#	$Id		移動先記事ID
#	*Move		移動する記事群のリストへのリファレンス
#
# - DESCRIPTION
#	指定された記事群を，指定された記事の下に移動する．
#	「下」がDB中で先か後かは，新着が上か下か，に依り異なる．
#
# - RETURN
#	なし
#
sub ReOrderArticleDb
{
    local( $Board, $Id, *Move ) = @_;

    local( $File, $TmpFile, $dId, $TopFlag );

    # 先頭フラグ
    $TopFlag = 1;

    $File = &GetPath( $Board, $DB_FILE_NAME );
    $TmpFile = &GetPath( $Board, "$DB_FILE_NAME.$TMPFILE_SUFFIX$$" );
    open( DBTMP, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    open( DB, "<$File" ) || &Fatal( 1, $File );
    while ( <DB> )
    {
	print( DBTMP "$_" ), next if ( /^\#/o );
	print( DBTMP "$_" ), next if ( /^$/o );

	# Idを取り出す
	chop;
	( $dId = $_ ) =~ s/\t.*$//;

	# 移動する奴は取り除く
	next if ( grep( /^$dId$/, @Move ));

	# 先頭にする時の処理(新着が下，の場合)
	if (( $Id eq '' ) && ( $SYS_BOTTOMTITLE == 1 ) && ( $TopFlag == 1 ))
	{
	    $TopFlag = 0;
	    foreach ( @Move )
	    {
		printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $_, $DB_FID{$_}, $DB_AIDS{$_}, $DB_DATE{$_}, $DB_TITLE{$_}, $DB_ICON{$_}, $DB_REMOTEHOST{$_}, $DB_NAME{$_}, $DB_EMAIL{$_}, $DB_URL{$_}, $DB_FMAIL{$_} );
	    }
	}

	# 移動先がきたら，先に書き込む(新着が上，の場合)
	if (( $SYS_BOTTOMTITLE == 0 ) && ( $dId eq $Id ))
	{
	    foreach ( @Move )
	    {
		printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $_, $DB_FID{$_}, $DB_AIDS{$_}, $DB_DATE{$_}, $DB_TITLE{$_}, $DB_ICON{$_}, $DB_REMOTEHOST{$_}, $DB_NAME{$_}, $DB_EMAIL{$_}, $DB_URL{$_}, $DB_FMAIL{$_} );
	    }
	}

	# DBに書き加える
	printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $dId, $DB_FID{$dId}, $DB_AIDS{$dId}, $DB_DATE{$dId}, $DB_TITLE{$dId}, $DB_ICON{$dId}, $DB_REMOTEHOST{$dId}, $DB_NAME{$dId}, $DB_EMAIL{$dId}, $DB_URL{$dId}, $DB_FMAIL{$dId} );

	# 移動先がきたら，続けて書き込む(新着が下，の場合)
	if (( $SYS_BOTTOMTITLE == 1 ) && ( $dId eq $Id ))
	{
	    foreach ( @Move )
	    {
		printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $_, $DB_FID{$_}, $DB_AIDS{$_}, $DB_DATE{$_}, $DB_TITLE{$_}, $DB_ICON{$_}, $DB_REMOTEHOST{$_}, $DB_NAME{$_}, $DB_EMAIL{$_}, $DB_URL{$_}, $DB_FMAIL{$_} );
	    }
	}
    }

    # 先頭にする時の処理(新着が上，の場合)
    if (( $Id eq '' ) && ( $SYS_BOTTOMTITLE == 0 ))
    {
	foreach ( @Move )
	{
	    printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $_, $DB_FID{$_}, $DB_AIDS{$_}, $DB_DATE{$_}, $DB_TITLE{$_}, $DB_ICON{$_}, $DB_REMOTEHOST{$_}, $DB_NAME{$_}, $DB_EMAIL{$_}, $DB_URL{$_}, $DB_FMAIL{$_} );
	}
    }

    # close Files.
    close DB;
    close DBTMP;

    # DBを更新する
    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );
}


###
## MakeArticleFile - 記事本文DBへの追加
#
# - SYNOPSIS
#	MakeArticleFile($TextType, $Article, $Id, $Board);
#
# - ARGS
#	$TextType	文書タイプ
#	$Article	本文
#	$Id		記事ID
#	$Board		掲示板ID
#
# - DESCRIPTION
#	記事本文DB(掲示板IDと同じ名前のディレクトリ)の中に，
#	IDと同じ名前のファイルとして，記事本文を保存する．
#
# - RETURN
#	なし
#
sub MakeArticleFile
{
    local( $TextType, $Article, $Id, $Board ) = @_;

    local( $File ) = &GetArticleFileName( $Id, $Board );

    open( TMP, ">$File" ) || &Fatal( 1, $File );
    printf( TMP "<!-- Kb-System-Id: %s/%s -->\n", $KB_VERSION, $KB_RELEASE);
    print( TMP "$Article\n" );
    close TMP;
}


###
## GetArticleBody - 記事本文DBの読み込み
#
# - SYNOPSIS
#	GetArticleBody($Id, $Board, *ArticleBody);
#
# - ARGS
#	$Id		記事ID
#	$BoardId	掲示板ID
#	*ArticleBody	本文各行を入れる配列変数へのリファレンス
#
# - DESCRIPTION
#	記事本文DB(掲示板IDと同じ名前のディレクトリ)の中の，
#	IDと同じ名前のファイルを読み出す．
#
# - RETURN
#	なし
#
sub GetArticleBody
{
    local( $Id, $Board, *ArticleBody ) = @_;

    local( $QuoteFile ) = &GetArticleFileName( $Id, $Board );
    open( TMP, "<$QuoteFile" ) || &Fatal( 1, $QuoteFile );
    while ( <TMP> )
    {
	push( @ArticleBody, $_ );
    }
    close TMP;
}


###
## GetArticleId - 記事番号DBの読み込み
#
# - SYNOPSIS
#	GetArticleId($Board);
#
# - ARGS
#	$Board		掲示板ID
#
# - DESCRIPTION
#	従来の最新記事IDを読み出す．
#
# - RETURN
#	最新記事のID
#
sub GetArticleId
{
    local( $Board ) = @_;

    local( $ArticleNumFile ) = &GetPath( $Board, $ARTICLE_NUM_FILE_NAME );
    local( $ArticleId );
    open( AID, "<$ArticleNumFile" ) || &Fatal( 1, $ArticleNumFile );
    chop( $ArticleId = <AID> );
    close AID;

    $ArticleId;
}


###
## WriteArticleId - 記事番号DBの更新
#
# - SYNOPSIS
#	WriteArticleId($Id, $Board);
#
# - ARGS
#	$Id		新規に書き込む記事番号
#	$Board		掲示板ID
#
# - DESCRIPTION
#	記事番号DBの更新
#
# - RETURN
#	なし
#
sub WriteArticleId
{
    local( $Id, $Board ) = @_;

    local( $File, $TmpFile, $OldArticleId );
    
    # 数字のくせに古い数値より若い! (数字じゃなきゃOK)
    $OldArticleId = &GetNewArticleId( $Board );
    &Fatal( 10, '' ) if (( $Id =~ /^\d+$/ ) && ( $Id < $OldArticleId ));

    $File = &GetPath( $Board, $ARTICLE_NUM_FILE_NAME );
    $TmpFile = &GetPath( $Board, "$ARTICLE_NUM_FILE_NAME.$TMPFILE_SUFFIX$$" );
    open( AID, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    print( AID "$Id\n" );
    close AID;

    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );
}


###
## GetArriveMailTo - 掲示板別新規メイル送信先DBの全読み込み
#
# - SYNOPSIS
#	GetArriveMailTo($CommentFlag, $Board, *ArriveMail);
#
# - ARGS
#	$CommentFlag	コメント行を含むか否か(0: 含まない, 1: 含む)
#	$Board		掲示板ID
#	*ArriveMail	送信先のメイルアドレスのリストのリファレンス
#
# - DESCRIPTION
#	掲示板別新規メイル送信先DBを，読み込む．
#	メイルアドレスが正しいか否か等のチェックは，一切行なわない．
#
# - RETURN
#	なし
#
sub GetArriveMailTo
{
    local($CommentFlag, $Board, *ArriveMail) = @_;
    local($ArriveMailFile);

    $ArriveMailFile = &GetPath( $Board, $ARRIVEMAIL_FILE_NAME );
    # ファイルがなきゃ空のまま
    open( ARMAIL, "<$ArriveMailFile" ) || return;
    while ( <ARMAIL> )
    {
	next if ((! $CommentFlag) && (/^\#/o || /^$/o));
	chop;
	push(@ArriveMail, $_);
    }
    close ARMAIL;
}


###
## UpdateArriveMailDb - 掲示板別新規メイル送信先DBの全更新
#
# - SYNOPSIS
#	UpdateArriveMailDb($Board, *ArriveMail);
#
# - ARGS
#	$Board		掲示板ID
#	*ArriveMail	送信先のメイルアドレスのリストのリファレンス
#			(コメント，空行も含まれる)
#
# - DESCRIPTION
#	掲示板別新規メイル送信先DBを，新たな送信先リストで一新する．
#	メイルアドレスが正しいか否か等のチェックは，一切行なわない．
#
# - RETURN
#	なし
#
sub UpdateArriveMailDb
{
    local( $Board, *ArriveMail ) = @_;

    local( $File ) = &GetPath( $Board, $ARRIVEMAIL_FILE_NAME );
    local( $TmpFile ) = &GetPath( $Board, $ARRIVEMAIL_FILE_NAME );
    open( DBTMP, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    foreach ( @ArriveMail )
    {
	print( DBTMP "$_\n" );
    }
    close DBTMP;
    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );
}


###
## CacheAliasData - ユーザDBの全読み込み
#
# - SYNOPSIS
#	CacheAliasData;
#
# - ARGS
#	なし
#
# - DESCRIPTION
#	ユーザエイリアスファイルを読み込んで連想配列に放り込む．
#	大域変数，%Name, %Email, %Host, %URLを破壊する．
#
# - RETURN
#	なし
#
sub CacheAliasData
{
    local( $A, $N, $E, $H, $U );

    # 放り込む．
    open( ALIAS, "<$USER_ALIAS_FILE" ) || &Fatal( 1, $USER_ALIAS_FILE );
    while ( <ALIAS> )
    {
	next if ( /^$/o || /^[^#]/ );
	chop;

	( $A, $N, $E, $H, $U ) = split( /\t/, $_ );

	$Name{$A} = $N;
	$Email{$A} = $E;
	$Host{$A} = $H;
	$URL{$A} = $U;
    }
    close ALIAS;
}


###
## GetUserInfo - ユーザDBの読み込み
#
# - SYNOPSIS
#	GetUserInfo($Alias);
#
# - ARGS
#	$Alias		検索するユーザID(エイリアス)
#
# - DESCRIPTION
#	ユーザDBから，ユーザの名前，メイル，URLを取ってくる．
#
# - RETURN
#	ユーザの名前，メイル，URLの順のリスト．
#
sub GetUserInfo
{
    local( $Alias ) = @_;

    local( $A, $N, $E, $H, $U, $rN, $rE, $rU );
    open( ALIAS, "<$USER_ALIAS_FILE" ) || &Fatal( 1, $USER_ALIAS_FILE );
    while ( <ALIAS> )
    {
	next if (/^$/o);
	chop;
	
	# 分割
	( $A, $N, $E, $H, $U ) = split( /\t/, $_ );
	
	# マッチしなきゃ次へ．
	next if ( $A ne $Alias );
	
	$rN = $N;
	$rE = $E;
	$rU = $U;
    }
    close ALIAS;

    ( $rN, $rE, $rU );
}


###
## WriteAliasData - ユーザDBの更新/追加
#
# - SYNOPSIS
#	WriteAliasData;
#
# - ARGS
#	なし
#
# - DESCRIPTION
#	ユーザエイリアスファイルにデータを書き出す．
#	%Name, %Email, %Host, %URLを必要とする．
#	$Nameが空だと書き込まない．
#
# - RETURN
#	なし
#
sub WriteAliasData
{
    local( $TmpFile ) = "$USER_ALIAS_FILE.$TMPFILE_SUFFIX$$";
    local( $Alias );

    open( ALIAS, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    printf( ALIAS "<!-- Kb-System-Id: %s/%s -->\n", $KB_VERSION, $KB_RELEASE );
    foreach $Alias ( sort keys( %Name ))
    {
	printf(ALIAS "%s\t%s\t%s\t%s\t%s\n", $Alias, $Name{$Alias}, $Email{$Alias}, $Host{$Alias}, $URL{$Alias}) if $Name{$Alias};
    }
    close ALIAS;

    rename( $TmpFile, $USER_ALIAS_FILE ) || &Fatal( 14, "$TmpFile -&gt; $USER_ALIAS_FILE" );
}


###
## GetAllBoardInfo - 掲示板DBの全読み込み
#
# - SYNOPSIS
#	GetAllBoardInfo(*BoardList, *BoardInfo);
#
# - ARGS
#	*BoardList	掲示板ID-掲示板名の連想配列のリファレンス
#	*BoardInfo	掲示板ID-掲示板情報の連想配列のリファレンス
#
# - DESCRIPTION
#	掲示板DBから，掲示板情報を取ってくる．
#
# - RETURN
#	なし
#
sub GetAllBoardInfo
{
    local( *BoardList, *BoardInfo ) = @_;

    local( $BoardId, $BName, $BInfo );

    open( ALIAS, "<$BOARD_ALIAS_FILE" ) || &Fatal( 1, $BOARD_ALIAS_FILE );
    while ( <ALIAS> )
    {
	next if ( /^\#/o || /^$/o );
	chop;
	( $BoardId, $BName, $BInfo ) = split( /\t/, $_, 3 );
	$BoardList{$BoardId} = $BName;
	$BoardInfo{$BoardId} = $BInfo;
    }
    close ALIAS;
}


###
## GetBoardInfo - 掲示板DBの読み込み
#
# - SYNOPSIS
#	GetBoardInfo($Alias);
#
# - ARGS
#	$Alias		掲示板ID
#
# - DESCRIPTION
#	掲示板DBから，掲示板情報を取ってくる．
#
# - RETURN
#	掲示板名
#
sub GetBoardInfo
{
    local( $Alias ) = @_;

    local( $dAlias, $dBoardName, $dBoardConf );

    open( ALIAS, "<$BOARD_ALIAS_FILE" ) || &Fatal( 1, $BOARD_ALIAS_FILE );
    while ( <ALIAS> )
    {
	next if ( /^\#/o || /^$/o );
	chop;
	( $dAlias, $dBoardName, $dBoardConf ) = split( /\t/, $_, 4 );
	if ( $Alias eq $dAlias ) {
	    close ALIAS;
	    return( $dBoardName, $dBoardConf );
	}
    }
    close ALIAS;

    &Fatal( 11, $Alias );
}


###
## CacheIconDb - アイコンDBの全読み込み
#
# - SYNOPSIS
#	CacheIconDb($Board);
#
# - ARGS
#	$Board		掲示板ID
#
# - DESCRIPTION
#	アイコンDBを読み込んで連想配列に放り込む．
#	大域変数，@ICON_TITLE，%ICON_FILE，%ICON_HELPを破壊する．
#
# - RETURN
#	なし
#
$ICON_DB_CACHE = 0;

sub CacheIconDb
{
    return if $ICON_DB_CACHE;

    local( $Board ) = @_;
    local( $FileName, $IconTitle, $IconHelp );

    @ICON_TITLE = %ICON_FILE = %ICON_HELP = ();
    open( ICON, &GetIconPath( "$Board.$ICONDEF_POSTFIX" ))
	|| ( open( ICON, &GetIconPath( "$DEFAULT_ICONDEF" ))
	    || &Fatal( 1, &GetIconPath( "$DEFAULT_ICONDEF" )));
    while ( <ICON> )
    {
	next if ( /^\#/o || /^$/o );
	chop;
	( $FileName, $IconTitle, $IconHelp ) = split( /\t/, $_, 3 );

	push( @ICON_TITLE, $IconTitle );
	$ICON_FILE{$IconTitle} = $FileName;
	$ICON_HELP{$IconTitle} = $IconHelp;
    }
    close ICON;

    $ICON_DB_CACHE = 1;		# cached
}


###
## GetBoardHeader - 掲示板ヘッダDBの読み込み
#
# - SYNOPSIS
#	GetBoardHeader($Board, *BoardHeader);
#
# - ARGS
#	$BoardId	掲示板ID
#	*BoardHeader	本文各行を入れる配列変数へのリファレンス
#
# - DESCRIPTION
#	掲示板ディレクトリの中の，掲示板ヘッダファイルを読み出す．
#
# - RETURN
#	なし
#
sub GetBoardHeader
{
    local( $Board, *BoardHeader ) = @_;

    local( $File ) = &GetPath( $Board, $BOARD_FILE_NAME );
    open( HEADER, "<$File" ) || &Fatal( 1, $File );
    while ( <HEADER> )
    {
	s/__PROGRAM__/$PROGRAM/g;
	push( @BoardHeader, $_ );
    }
    close HEADER;
}


###
## GetArticleFileName - 記事本文DBファイルのパス名の取得
#
# - SYNOPSIS
#	GetArticleFileName($Id, $Board);
#
# - ARGS
#	$Id		記事ID
#	$Board		掲示板ID
#
# - DESCRIPTION
#	掲示板IDと記事IDから，記事DB中の，記事ファイルのパス名を作り出す．
#	大域変数$MACPERLを参照し，MacPerlに対応．
#
# - RETURN
#	パスを表す文字列
#
sub GetArticleFileName
{
    local( $Id, $Board ) = @_;

    # Boardが空ならBoardディレクトリ内から相対，
    # 空でなければシステムから相対
    if ( $MACPERL )
    {
	$Board? ":$Board:$Id" : "$Id";
    }
    else
    {
	$Board? "$Board/$Id" : "$Id";
    }
}


###
## GetPath - DBファイルのパス名の取得
#
# - SYNOPSIS
#	GetPath($DbDir, $File);
#
# - ARGS
#	$DbDir		DBディレクトリ(掲示板ID, etc.)
#	$File		ファイル名
#
# - DESCRIPTION
#	DBディレクトリ名とファイル名から，DBファイルのパス名を作り出す．
#	大域変数$MACPERLを参照し，MacPerlに対応．
#
# - RETURN
#	パスを表す文字列
#
sub GetPath
{
    local( $DbDir, $File ) = @_;

    if ( $MACPERL )
    {
	":$DbDir:$File";
    }
    else
    {
	"$DbDir/$File";
    }
}


###
## GetIconPath - アイコンgifDBファイルのパス名の取得
#
# - SYNOPSIS
#	GetIconPath($File);
#
# - ARGS
#	$File		アイコンgifファイル名
#
# - DESCRIPTION
#	アイコンgifファイルのパス名を作り出す．
#	大域変数$MACPERLを参照し，MacPerlに対応．
#
# - RETURN
#	パスを表す文字列
#
sub GetIconPath
{
    local( $File ) = @_;

    if ( $MACPERL )
    {
	":$ICON_DIR:$File";
    }
    else
    {
	"$ICON_DIR/$File";
    }
}


###
## GetIconUrlFromTitle - アイコンgifのURLの取得
#
# - SYNOPSIS
#	GetIconUrlFromTitle($Icon, $Board);
#
# - ARGS
#	$Icon		アイコンID
#	$Board		掲示板ID
#
# - DESCRIPTION
#	アイコンIDから，そのアイコンに対応するgifファイルのURLを取得．
#
# - RETURN
#	URLを表す文字列
#
sub GetIconUrlFromTitle
{
    local( $Icon, $Board ) = @_;

    local( $FileName, $Title, $TargetFile );

    open( ICON, &GetIconPath( "$Board.$ICONDEF_POSTFIX" ))
	|| ( open( ICON, &GetIconPath( "$DEFAULT_ICONDEF" ))
	    || &Fatal( 1, &GetIconPath( "$DEFAULT_ICONDEF" )));
    while ( <ICON> )
    {
	next if ( /^\#/o || /^$/o );
	chop;
	( $FileName, $Title ) = split( /\t/, $_, 3 );
	if ( $Title eq $Icon ) { $TargetFile = $FileName; }
    }
    close ICON;

    $TargetFile? "$ICON_DIR/$TargetFile" : '';
}


###
## SupersedeDbFile - 訂正記事の記事DBへの書き込み
#
# - SYNOPSIS
#	SupersedeDbFile($Board, $Id, $InputDate, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $Fmail);
#
# - ARGS
#	$Board		訂正する記事が含まれる掲示板のID
#	$Id		訂正する記事のID
#	$InputDate	訂正時間(UTC)
#	$Subject	訂正記事Subject
#	$Icon		訂正記事アイコン
#	$RemoteHost	訂正記事書き込みホスト名
#	$Name		訂正記事書き込みユーザ名
#	$Email		訂正記事書き込みユーザメイルアドレス
#	$Url		訂正記事書き込みユーザURL
#	$Fmail		リプライ時にメイルを送信するか否か
#
# - DESCRIPTION
#	訂正記事をDBファイルに書き込み，agingした記事のIDを返す．
#
# - RETURN
#	agingした記事のID．
#
sub SupersedeDbFile
{
    local( $Board, $Id, $InputDate, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $Fmail ) = @_;

    local( $SupersedeId, $dId, $dFid, $dAids, $dInputDate, $dSubject, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail, $File, $TmpFile );
    
    # initial versionは1で，1ずつ増えていく．1，2，…9，10，11，…
    # later versionはDB中で必ず，younger versionよりも下に出現する．
    # すなわち10_2，10，10_1は，10_1，10_2，10の順に並ぶものとする．
    $SupersedeId = 1;

    $File = &GetPath( $Board, $DB_FILE_NAME );
    $TmpFile = &GetPath( $Board, "$DB_FILE_NAME.$TMPFILE_SUFFIX$$" );
    open( DBTMP, ">$TmpFile" ) || &Fatal( 1, $TmpFile );
    open( DB, "<$File" ) || &Fatal( 1, $File );
    while ( <DB> )
    {
	print( DBTMP "$_" ), next if ( /^\#/o || /^$/o );
	chop;

	( $dId, $dFid, $dAids, $dInputDate, $dSubject, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail ) = split( /\t/, $_ );

	# later versionが見つかったら，versionを先読みしておく．
	$SupersedeId++ if ( "$dId" eq ( sprintf( "#-%s_%s", $Id, $SupersedeId )));

	# 訂正記事の最新版が見つかったら，
	if ( $dId eq $Id )
	{
	    # agingしてしまう
	    printf( DBTMP "#-%s_%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $dId, $SupersedeId, $dFid, $dAids, $dInputDate, $dSubject, $dIcon, $dRemoteHost, $dName, $dEmail, $dUrl, $dFmail );

	    # 続いて新しい記事を書き加える
	    printf( DBTMP "%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\t%s\n", $Id, $dFid, $dAids, $InputDate, $Subject, $Icon, $RemoteHost, $Name, $Email, $Url, $Fmail );
	}
	else
	{
	    # DBに書き加える
	    print( DBTMP "$_\n" );
	}
    }

    # close Files.
    close DB;
    close DBTMP;

    # DBを更新する
    rename( $TmpFile, $File ) || &Fatal( 14, "$TmpFile -&gt; $File" );

    # 返す
    $SupersedeId;
}
